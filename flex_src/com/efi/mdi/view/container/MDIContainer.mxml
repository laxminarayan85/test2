<?xml version="1.0" encoding="utf-8"?>
<mx:Panel  
	xmlns:menu="com.efi.mdi.view.header.*" 
	xmlns:window="com.efi.mdi.view.window.*"
	xmlns:task="com.efi.mdi.view.taskbar.*"
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="vertical" 
	width="100%" height="100%"
	styleName="MDIContainerStyle"
	creationComplete="mdiCreationComplete();"
	>
	<mx:Style source="/assets/styles/style.css"/>
	
	<mx:Script source="../../../printsmith/common/scripts/CommonUtils.as"/>
	
	<mx:Script>
		<![CDATA[
			import com.efi.mdi.event.compare.MDICompareObjectEvent;
			import com.efi.mdi.compare.ObjectCompareUtil;
			import com.efi.mdi.event.window.WindowSaveSuccessEvent;
			import com.efi.mdi.event.window.WindowConfirmStatusEvent;
			import com.efi.mdi.view.window.MDIConfirmComponent;
			import com.efi.mdi.IMDIWindowAware;
			import com.efi.mdi.ContainerManager;
			import mx.managers.CursorManager;
			import com.efi.mdi.event.window.WindowResizeEvent;
			import com.efi.mdi.event.window.WindowResizeStartEvent;
			import com.efi.mdi.event.window.WindowRestoreEvent;
			import com.efi.mdi.event.window.WindowCloseEvent;
			import com.efi.mdi.view.taskbar.MinimizedWindow;
			import com.efi.mdi.event.window.WindowMinimizeEvent;
			import mx.core.UIComponent;
			import com.efi.mdi.view.header.ApplicationMenuBar;
			import mx.managers.PopUpManager;
			import com.efi.mdi.view.window.MDIWindow;
			import mx.controls.Alert;
			import mx.events.MenuEvent;
			import com.efi.mdi.event.menu.AppMenuItemClickedEvent;
			import mx.collections.ArrayCollection;
		
			[Bindable] private var _menuData:ArrayCollection;
			
			[Bindable]
	        [Embed("/assets/mdi/resize_arrow_orange.png")]
	        private var resizeCursor:Class;
			
			private var _windowResizeInProgress:MDIWindow;
			private var _windowMenuLabel:String;
			
			public function set windowMenuLabel(val:String):void	{
				_windowMenuLabel = val;
			}
			
			public function set menuData(val:ArrayCollection):void	{
				_menuData = val;	
			}
			 
			private function onMenuItemClicked(event:AppMenuItemClickedEvent):void	{
				dispatchEvent(event);
			}
			
			public function addMDIWindow(mdiWin:MDIWindow):void	{
				
				mdiWin.addEventListener(WindowMinimizeEvent.WINDOW_MINIMIZE, onMinimize);
				mdiWin.addEventListener(WindowCloseEvent.WINDOW_CLOSE, onClose);
				mdiWin.addEventListener(WindowResizeStartEvent.WINDOW_RESIZE_START, onWindowResizeStart);
				mdiWin.addEventListener(MDICompareObjectEvent.MDI_COMPAREOBJECT,onCompareObject);
				
				appCanvas.addChild(mdiWin);
				menubar.addWindow(mdiWin);		
				mdiWin.bringWindowToFront();
														
			}
			
			public function addToHeader(position:int, content:UIComponent):void	{
				header.addItem(position, content);
			}
			
			private function mdiCreationComplete():void	{
				menubar.addEventListener(AppMenuItemClickedEvent.APP_MENU_ITEM_CLICKED, onMenuItemClicked);
				this.addEventListener(WindowResizeEvent.WINDOW_RESIZE, onWindowResizeStop);
				this.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoveAfterResize);	
				this.addEventListener(MouseEvent.MOUSE_UP, onMouseUpAfterResize);
			}
			
			private function onMinimize(event:WindowMinimizeEvent):void	{
				var minWin:MinimizedWindow = new MinimizedWindow();
				minWin.windowRef = event.mdiWindow;
				minWin.addEventListener(WindowCloseEvent.WINDOW_CLOSE, onClose);
				minWin.addEventListener(WindowRestoreEvent.WINDOW_RESTORE, onRestore);
				event.mdiWindow.visible = false;
				event.mdiWindow.state = MDIWindow.MINIMIZED;
				taskbar.addChild(minWin);
				ContainerManager.getExistingInstance().recalculateWindowStyles();
			}
			
			private function closeMinimizedWindow(minWin:MinimizedWindow, isRoot:Boolean = false):void	{
				closeWindow(minWin.windowRef, isRoot);
				taskbar.removeChild(minWin);				
			}
			private function closeWindow(window:MDIWindow, isRoot:Boolean = false):void	{
				
				window.state = MDIWindow.CLOSED;
				
				if (window.closeChildren)	{
					var ac:ArrayCollection = ContainerManager.getExistingInstance().getChildrenOfWindow(window.windowID);
					if (ac != null)	{
						for (var i:int=0; i<ac.length; i++)	{
							var win:MDIWindow = ac.getItemAt(i) as MDIWindow;
							if (win.state == MDIWindow.MINIMIZED)	{
								var min:MinimizedWindow = ContainerManager.getExistingInstance().getMinimized(win);
								if (min)
									closeMinimizedWindow(min);
							}
							else	{
								closeWindow(win);
							}
							
						}						
					} 
				}
				appCanvas.removeChild(window);
				
				if (isRoot)	{
					ContainerManager.getExistingInstance().recalculateWindowStyles();
					ContainerManager.getExistingInstance().removeChildFromTree(window);
				}
				
				
			}
			
			private function onClose(event:WindowCloseEvent):void	{
				if (event.child is MDIWindow)	{
					var mdiWindow:MDIWindow = event.child as MDIWindow;
					if(mdiWindow.content is IMDIWindowAware){
						var mdiWindowAware:IMDIWindowAware = mdiWindow.content as IMDIWindowAware;
						mdiWindowAware.onClose();
						if(mdiWindowAware.getOriginalObject()!=null && mdiWindowAware.getUpdatedObject()!=null){
							if(ObjectCompareUtil.compare(mdiWindowAware.getOriginalObject(),mdiWindowAware.getUpdatedObject())!=0){
								var mdiConfirmComponent:MDIConfirmComponent = PopUpManager.createPopUp(this,MDIConfirmComponent,true) as MDIConfirmComponent;
								mdiConfirmComponent.labelTxt = mdiWindowAware.getConfirmationText();
								mdiConfirmComponent.addEventListener(WindowConfirmStatusEvent.WINDOW_CONFIRM_STATUS,function(event:WindowConfirmStatusEvent):void{
									if(event.windowConfirmStatus==1){
										mdiWindowAware.onDontSave();
										closeWindow(mdiWindow, true);
									} else if(event.windowConfirmStatus==2) {
										mdiWindowAware.onCancel();
									} else if(event.windowConfirmStatus==3) {
										mdiWindow.content.addEventListener(WindowSaveSuccessEvent.WINDOW_SAVESUCCESS_STATUS,function(event:WindowSaveSuccessEvent):void {
											if(event.isSaved){
												closeWindow(mdiWindow,true);
											}	
										},false,0,true);
										mdiWindowAware.onSave();
									}
								},false,0,true);
							} else {
								closeWindow(event.child as MDIWindow, true); 
							}
						}
					} else {
						closeWindow(event.child as MDIWindow, true); 
					}
				}
				if (event.child is MinimizedWindow)	{
					closeMinimizedWindow(event.child as MinimizedWindow, true);
				}	
			}
			
			private function onCompareObject(event:MDICompareObjectEvent):void {
				var functionName:Function = event._functionName;
				if(event._content is IMDIWindowAware){
					var _content:UIComponent = event._content;
					var inputArguments:Array = event._inputArguments;
					var mdiWindowAware:IMDIWindowAware = event._content as IMDIWindowAware;
					mdiWindowAware.onClose();
					if(mdiWindowAware.getOriginalObject()!=null && mdiWindowAware.getUpdatedObject()!=null){
						if(ObjectCompareUtil.compare(mdiWindowAware.getOriginalObject(),mdiWindowAware.getUpdatedObject())!=0){
							var mdiConfirmComponent:MDIConfirmComponent = PopUpManager.createPopUp(this,MDIConfirmComponent,true) as MDIConfirmComponent;
							mdiConfirmComponent.labelTxt = mdiWindowAware.getConfirmationText();
							mdiConfirmComponent.addEventListener(WindowConfirmStatusEvent.WINDOW_CONFIRM_STATUS,function(event:WindowConfirmStatusEvent):void{
								if(event.windowConfirmStatus==1){
									mdiWindowAware.onDontSave();
									if(inputArguments!=null && inputArguments.length>0){
										callLater(functionName,inputArguments);
									} else {
										callLater(functionName);	
									}
								} else if(event.windowConfirmStatus==2) {
									mdiWindowAware.onCancel();
								} else if(event.windowConfirmStatus==3) {
									_content.addEventListener(WindowSaveSuccessEvent.WINDOW_SAVESUCCESS_STATUS,function(event:WindowSaveSuccessEvent):void {
										if(event.isSaved){
											if(inputArguments!=null && inputArguments.length>0){
												callLater(functionName,inputArguments);
											} else {
												callLater(functionName);	
											}
										}	
									},false,0,true);
									mdiWindowAware.onSave();
								}
							},false,0,true);
						} else {
							if(inputArguments!=null && inputArguments.length>0){
								callLater(functionName,inputArguments);
							} else {
								callLater(functionName);	
							}
						}
					}  else {
						if(inputArguments!=null && inputArguments.length>0){
							callLater(functionName,inputArguments);
						} else {
							callLater(functionName);	
						}
					}
				} else {
					if(inputArguments!=null && inputArguments.length>0){
						callLater(functionName,inputArguments);
					} else {
						callLater(functionName);	
					}
				}
			}
			
			private function onWindowResizeStart(event:WindowResizeStartEvent):void	{
				_windowResizeInProgress = event.mdiWindow;
				cursorManager.setCursor(resizeCursor); 
							
			}
			
			private function onMouseMoveAfterResize(event:MouseEvent):void	{
				if (_windowResizeInProgress != null)	{
					cursorManager.setCursor(resizeCursor); 
					dispatchEvent(new WindowResizeEvent(WindowResizeEvent.WINDOW_RESIZE, _windowResizeInProgress, event.stageX, event.stageY));
				}
			}
			
			private function onMouseUpAfterResize(event:MouseEvent):void	{
				if (_windowResizeInProgress != null)	{
					cursorManager.removeAllCursors(); //.removeBusyCursor();
					_windowResizeInProgress = null;
				}
			}
			
			private function onWindowResizeStop(event:WindowResizeEvent):void	{
				
				var currX:int = event.mouseX;
				var currY:int = event.mouseY;
				
				var r:Rectangle = ContainerManager.getExistingInstance().getCanvasRectangle();
				if (event.mouseX > r.width) 
					currX = r.width;
				
				if (event.mouseY > r.height)		
					currY = r.height;
				
				
				event.mdiWindow.width = currX - event.mdiWindow.x;
				event.mdiWindow.height = currY - event.mdiWindow.y;	
				if (event.mdiWindow.width < 200)
					event.mdiWindow.width = 200;
				if (event.mdiWindow.height < 200)
					event.mdiWindow.height = 200;
								
			}
			
			private function onRestore(event:WindowRestoreEvent):void	{
				event.minWindow.windowRef.visible = true;
				event.minWindow.windowRef.state = MDIWindow.OPEN;
				event.minWindow.windowRef.bringWindowToFront();
				taskbar.removeChild(event.minWindow);
				
			}
		]]>
	</mx:Script>
	
	<mx:HBox height="4%" width="100%" borderThickness="0"
		borderStyle="solid" styleName="headerBoxStyle" >
		<menu:ApplicationMenuBar id="menubar" menuData="{_menuData}"  windowMenuLabel="{_windowMenuLabel}" height="100%" width="80%" />
		<menu:ApplicationHeader id="header"  height="100%" width="20%" />
	</mx:HBox>
			
	<mx:VDividedBox id="divbox" height="96%" width="100%"  >
		<window:ApplicationCanvas id="appCanvas"  height="100%" width="100%" />
		<task:ApplicationTaskBar height="5%" width="100%" id="taskbar" />
	</mx:VDividedBox> 
</mx:Panel>

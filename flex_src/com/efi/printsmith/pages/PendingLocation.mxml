<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="412" height="350"
	layout="vertical" showCloseButton="true" creationComplete="{init()}" close="{closeWindow()}"
    title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'jobCmd.Location')}"
    paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5"
    xmlns:fc="http://www.adobe.com/2006/fc" xmlns:components="com.efi.printsmith.common.components.*">
    
    <mx:Script source="../common/scripts/CommonUtils.as"/>
    
    <mx:Script source="../common/scripts/ComponentHelper.as"/>
    
    <mx:Script>
    	<![CDATA[
    		import com.efi.printsmith.data.HoldState;
    		import com.efi.printsmith.events.PendingListUpdateEvent;
    		import com.efi.printsmith.data.Estimate;
    		import com.efi.printsmith.data.Invoice;
    		import flash.net.registerClassAlias;
    		import mx.utils.ObjectUtil;
    		import com.efi.printsmith.data.ProductionLocations;
    		import mx.collections.ArrayCollection;
    		import mx.utils.StringUtil;
    		import mx.controls.Alert;
    		import mx.rpc.events.FaultEvent;
    		import mx.rpc.events.ResultEvent;
    		import com.efi.printsmith.data.JobBase;
    		import com.efi.printsmith.data.InvoiceBase;
    		import mx.managers.PopUpManager;
    		
    		[Bindable] private var _invoiceBase:InvoiceBase;
    		
    		[Bindable] private var location:String = "";
    		
    		[Bindable] private var jobsList:ArrayCollection = new ArrayCollection();
    		
    		[Bindable] private var productionLocationsList:Array = new Array();
    		
    		[Bindable] private var holdStateList:Array = new Array();
    		
    		[Bindable] private var selectedJobBase:JobBase;
    		
    		[Bindable] private var enabledValue:Boolean = true;
    		
    		public var pendingStatus:PendingStatus;
    		
    		private var updateLocationDate:Boolean = false;
    		
    		private function init():void {
    			PopUpManager.centerPopUp(this);
    		}
    		
    		private function closeWindow():void {
    			if(pendingStatus!=null && pendingStatus.isPopUp) {
    				PopUpManager.removePopUp(pendingStatus);
    			}
    			PopUpManager.removePopUp(this);
    		}
    		
    		public function get invoiceBase():InvoiceBase {
				return _invoiceBase;	
			}
			
			public function set invoiceBase(tmpInvoiceBase:InvoiceBase):void {
				registerClassAlias("com.efi.printsmith.data.InvoiceBase",InvoiceBase);
				_invoiceBase = ObjectUtil.copy(tmpInvoiceBase) as InvoiceBase;
				dataService.getAll("ProductionLocations");
				dataServiceState.getAll("HoldState");
			}
			
			private function handleLoadResult(event:ResultEvent):void {
				_invoiceBase = event.result as InvoiceBase;
				if(_invoiceBase!=null && _invoiceBase.holdState!=null){
					holdstate_combo.selectedIndex = findItem(_invoiceBase.holdState.id,holdStateList);
				} else {
					holdstate_combo.selectedIndex = findItem(-1,holdStateList);
				}
				getDocumentLocation();
				jobsList = new ArrayCollection();
				if(_invoiceBase.jobs!=null) {
					jobsList = _invoiceBase.jobs;
				}
				var jobBase:JobBase = new JobBase();
				jobBase.id=0;
				jobBase.description = resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.SelectJob');
				jobsList.addItemAt(jobBase,0);
				if(jobsList.length>1){
					job_combo.selectedIndex = 1;
					var jobBaseObj:JobBase = jobsList.getItemAt(1) as JobBase;
					selectedJobBase = jobBaseObj;
					if(selectedJobBase.location!=null){
						location_combo.selectedIndex = findItem(selectedJobBase.location.id,productionLocationsList);
					} else {
						location_combo.selectedIndex = findItem(-1,productionLocationsList);
					}
					/* if(jobBaseObj.location!=null){
						job_location.selectedIndex = findItem(jobBaseObj.location.id,masterProductionLocationsList);
					} else {
						job_location.selectedIndex = -1;
					} */
				} else {
					enabledValue = false;
				}
			}
			
			private function handleLoadLocationsResult(event:ResultEvent):void {
				var arrayList:ArrayCollection = event.result as ArrayCollection;
				productionLocationsList = new Array();
				var emptyObj:ProductionLocations = new ProductionLocations();
				emptyObj.id = -1;
				emptyObj.name = "None";
				productionLocationsList.push(emptyObj);
				if(arrayList!=null){
					for each(var obj:Object in arrayList){
						productionLocationsList.push(obj);
					}
				}
				dataService.getInvoice(_invoiceBase.id);
			}
			
			private function handleLoadHoldStateResult(event:ResultEvent):void {
				var arrayList:ArrayCollection = event.result as ArrayCollection;
				holdStateList = new Array();
				var emptyObj:HoldState = new HoldState();
				emptyObj.id = -1;
				emptyObj.name = "None";
				holdStateList.push(emptyObj);
				if(arrayList!=null){
					for each(var obj:Object in arrayList){
						holdStateList.push(obj);
					}
				}
				if(_invoiceBase!=null && _invoiceBase.holdState!=null){
					holdstate_combo.selectedIndex = findItem(_invoiceBase.holdState.id,holdStateList);
				} else {
					holdstate_combo.selectedIndex = findItem(-1,holdStateList);
				}
			}
			
			private function handleSaveResult(event:ResultEvent):void {
				if(_invoiceBase is Invoice){
					openConfirmationComponent(StringUtil.substitute(resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'CommonConfirmation.SAVE'),resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'newInvCmd.Invoice')),Snowmass.getInstance());
				} else if(_invoiceBase is Estimate){
					openConfirmationComponent(StringUtil.substitute(resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'CommonConfirmation.SAVE'),resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'newInvCmd.Estimate')),Snowmass.getInstance());
				}
				dispatchEvent(new PendingListUpdateEvent(PendingListUpdateEvent.PENDINGLIST_UPDATE_EVENT,true));
				callLater(closeWindow);
			}
			
			private function findItem(id:int, array:Array):int
			{
				for each (var element:Object in array)
				{
					if (id == element.id)
					{
						return array.indexOf(element);
					}
				}
				return -1;
			}
			
			private function handleFault(ev:FaultEvent):void {  
				var errorDetail:String="";
				if (ev.fault!=null && ev.fault.faultCode=="InvalidSecurityAccess"){
					errorDetail = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, ev.fault.faultDetail);
					Alert.show(errorDetail,ev.fault.faultCode, Alert.OK, null, null, null, Alert.OK);
				}else{
					var message:String;
					              
					message = "Error: "                          
					+ ev.fault.faultCode + " - "                                  
					+ ev.fault.faultDetail + " - "                                  
					+ ev.fault.faultString;
					Alert.show(message, "Error", 0, null, null, null,4);                                 
				}
			}
			
			private function getDocumentLocation():void {
				if(_invoiceBase.jobs!=null && _invoiceBase.jobs.length>0){
					for each(var job:JobBase in  _invoiceBase.jobs){
						if(job.location!=null){
							location = job.location.name;
							doc_location.selectedIndex = findItem(job.location.id,productionLocationsList);
						}
						break;
					}
				}
			}
			
			private function getJobsSize(invoiceBaseObj:InvoiceBase):String {
				var jobSize:int=0;
				if(invoiceBaseObj!=null){
					if(invoiceBaseObj.jobs!=null){
						jobSize = invoiceBaseObj.jobs.length;
					}
				}
				return StringUtil.substitute(resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.Jobs0'),jobSize);
			}
			
			private function jobLabelFunc(item:Object):String {
				if(item!=null){
					if(item.id==0){
						return item.description;
					} else {
						if(item.location==null){
							return item.description;
						} else {
							return item.description+" @ "+item.location.name;
						}
					}
				}
				return "";
			}
			
			private function setJobLocation(event:Event):void {
				var jobBaseObj:JobBase = event.currentTarget.selectedItem as JobBase;
				if(jobBaseObj.id==0){
					enabledValue = false;
				} else {
					enabledValue = true;
				}
				selectedJobBase = jobBaseObj;
				if(selectedJobBase.location!=null){
					location_combo.selectedIndex = findItem(selectedJobBase.location.id,productionLocationsList);
				} else {
					location_combo.selectedIndex = findItem(-1,productionLocationsList);
				}
			}
			
			private function checkStateValue():void {
				if(_invoiceBase!=null && _invoiceBase.holdState!=null){
					_invoiceBase.readyToPickup = false;
				}
			}
			
			private function setProductionLocation():void {
				if(selectedJobBase!=null) {
					if(location_combo.selectedItem!=null){
						if(location_combo.selectedItem.id==-1) {
							selectedJobBase.location = null;
						} else {
							selectedJobBase.location = location_combo.selectedItem as ProductionLocations;
						}
					} else {
						selectedJobBase.location = null;
					}
				}
				updateLocationDate=true;
			}
			
			private function setHoldState():void {
				if(_invoiceBase!=null) {
					if(holdstate_combo.selectedItem!=null){
						if(holdstate_combo.selectedItem.id==-1) {
							_invoiceBase.holdState = null;
						} else {
							_invoiceBase.holdState = holdstate_combo.selectedItem as HoldState;
						}
					} else {
						_invoiceBase.holdState = null;
					}
				}
			}
				
			
			private function doSave():void {
				if(_invoiceBase!=null) {
					var invoiceJobList:ArrayCollection = new ArrayCollection();
					registerClassAlias("com.efi.printsmith.data.JobBase",JobBase);
					invoiceJobList = ObjectUtil.copy(jobsList) as ArrayCollection
					invoiceJobList.removeItemAt(0);
					_invoiceBase.jobs = invoiceJobList;
					if(updateLocationDate) {
						_invoiceBase.locationChangeDate = new Date();
					}
					dataService.saveInvoice(_invoiceBase);
				}
			}
			
			private function getTitle(invoiceBaseObj:InvoiceBase):String {
				if(invoiceBaseObj is Invoice){
					return resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'rStandardMenuText.Invoice')
				} else if(invoiceBaseObj is Estimate){
					return resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'newInvCmd.Estimate');
				}
				return "";
			}
			
			private function updateCommentDate():void {
				if(selectedJobBase!=null) {
					selectedJobBase.lastUpdatedCommentDate = new Date();
				}
			}
			
			private function setCompletedDate():void {
				if(_invoiceBase!=null) {
					if(_invoiceBase.readyToPickup) {
						_invoiceBase.completedDate = new Date();
					} else {
						_invoiceBase.completedDate = null;
					}
				}
			}
	
    	]]>
    </mx:Script>

	<mx:RemoteObject id="dataService" destination="dataService" showBusyCursor="true">
		<mx:method name="getAll" fault="handleFault(event)" result="handleLoadLocationsResult(event)"/>
		<mx:method name="getInvoice" fault="handleFault(event)" result="handleLoadResult(event)"/>
		<mx:method name="saveInvoice" fault="handleFault(event)" result="handleSaveResult(event)"/>
	</mx:RemoteObject>
	
	<mx:RemoteObject id="dataServiceState" destination="dataService" showBusyCursor="true">
		<mx:method name="getAll" fault="handleFault(event)" result="handleLoadHoldStateResult(event)"/>
	</mx:RemoteObject>

	<mx:Label text="{getTitle(_invoiceBase)}: {_invoiceBase.invoiceNumber} {_invoiceBase.name}"/>
	
	<mx:Label text="(#{_invoiceBase.account.accountId}) {_invoiceBase.account.title}"/>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Label text="{getJobsSize(_invoiceBase)}:" width="30%" textAlign="right"/>
		<mx:ComboBox id="job_combo" dataProvider="{jobsList}" labelFunction="jobLabelFunc" width="60%"
			change="{setJobLocation(event)}"/>
	</mx:HBox>
	
	<mx:HRule width="100%"/>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.DocumentLocation')}:" width="30%" textAlign="right"/>
		<fc:AutoComplete id="doc_location" dataProvider="{productionLocationsList}" labelField="name" enabled="false" width="60%" disabledColor="#000000"/>
	</mx:HBox>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.JobLocation')}:" width="30%" textAlign="right"/>
		<components:EditableComboBox id="location_combo" enabled="{enabledValue}" change="{setProductionLocation()}"
			dataProvider="{productionLocationsList}" labelField="name" editable="false" optional="true"/>
		<!--<components:CustomAutoCompleteComponent enabled="{enabledValue}"
			labelFieldId="id" labelField="name" updateObject="true" objectLabelFieldId="id" masterList="{productionLocationsList}"
			dataHolder="{selectedJobBase}" variableName="location" customData="{selectedJobBase.location}" customText="{getCustomTextForAutocomplete(productionLocationsList,selectedJobBase,'location','id','name',true,'id')}"/>-->
		<!--<fc:AutoComplete id="job_location" dataProvider="{productionLocationsList}" lookAhead="true" labelField="name" width="60%" focusOut="{updateJobLocation(event)}"/>-->
	</mx:HBox>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.Comment')}:" width="30%" textAlign="right"/>
		<components:CustomTextAreaComponent
			dataHolder="{selectedJobBase}" variableName="comment" customText="{selectedJobBase.comment}"
			width="60%" height="10%" enabled="{enabledValue}" disabledColor="#000000" change="{updateCommentDate()}"/>
	</mx:HBox>
	
	<mx:HRule width="100%"/>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.HoldState')}:" width="30%" textAlign="right"/>
		<components:EditableComboBox id="holdstate_combo" change="{setHoldState()}" focusOut="{checkStateValue()}"
			dataProvider="{holdStateList}" labelField="name" editable="false" optional="true"/>
		<!--<components:CustomAutoCompleteComponent focusOut="{checkStateValue()}"
			labelFieldId="id" labelField="name" updateObject="true" objectLabelFieldId="id" masterList="{holdStateList}"
			dataHolder="{_invoiceBase}" variableName="holdState" customData="{_invoiceBase.holdState}" customText="{getCustomTextForAutocomplete(holdStateList,_invoiceBase,'holdState','id','name',true,'id')}"/>-->
	</mx:HBox>
	
	<mx:HBox width="100%" horizontalAlign="right">
		<mx:Spacer width="30%"/>
		<components:CustomCheckBoxComponent width="70%"
			label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'posDoneCmd.Readyforpickupdelive')}"
			dataHolder="{_invoiceBase}" variableName="readyToPickup" customText="{_invoiceBase.readyToPickup}"
			defaultValue="false" selectedValue="true" deSelectedValue="false" enabled="{_invoiceBase.holdState==null}" disabledColor="#000000"
			click="{setCompletedDate()}"/>
	</mx:HBox>
	
	<mx:HBox width="100%">
		<mx:HBox width="100%" horizontalAlign="left">
			<mx:Button label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'rButtonLabel.OK')}" click="{doSave()}"/>
	    </mx:HBox>
		<mx:HBox width="100%" horizontalAlign="right">
			<mx:Button label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'rButtonLabel.Cancel')}" click="{closeWindow()}"/>
	    </mx:HBox>
    </mx:HBox>               
</mx:TitleWindow>
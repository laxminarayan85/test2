<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="890" height="430" creationComplete="init()" 
 close="PopUpManager.removePopUp(this);"
	xmlns:fc="http://www.adobe.com/2006/fc" xmlns:vo="com.efi.printsmith.data.*"  showCloseButton="true"
	title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.StockOrder')}" 
	label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.StockOrder')}" 
	name="StockOrderReport">
	
	<mx:RemoteObject id="dataService" destination="dataService">
		<mx:method name="getAll" fault="handleFault(event)" result="handleLoadResult(event)"/>
		<mx:method name="addUpdate" fault="handleFault(event)" result="handleSaveResult(event)"/>
		<mx:method name="deleteItem" fault="handleFault(event)" result="handleDeleteResult(event)"/>
	</mx:RemoteObject>
	<mx:RemoteObject id="xpedxIntegrationService" destination="xpedxIntegrationService">
		<mx:method name="submitOrder"					fault="handleFault(event)"
					result="handleSubmitOrderResult(event)"/>
	</mx:RemoteObject>
	
	
<mx:Script>                                   
<![CDATA[
	import com.efi.printsmith.pages.StockOrderComments;
	import com.efi.printsmith.pages.QuantityUpdate;
	import com.efi.printsmith.data.StockOrder;
	import mx.controls.DateField;                               
	import mx.managers.PopUpManager;                                         
	import mx.containers.TitleWindow;                                         
	import mx.collections.ArrayCollection;                                         
	import mx.rpc.events.ResultEvent;                                         
	import mx.rpc.events.FaultEvent;
	import mx.rpc.*; 
	import mx.controls.Alert;  
	import mx.utils.StringUtil;                                       
	import mx.messaging.messages.AsyncMessage;
	import mx.messaging.messages.IMessage;
    import mx.printing.FlexPrintJob;
    import mx.printing.PrintAdvancedDataGrid;
    import mx.printing.PrintDataGrid;
    import mx.core.Application;
    
    import com.efi.printsmith.data.Vendor;
    import com.efi.printsmith.events.QuantityEvent;
    import com.efi.printsmith.pages.QuantityUpdate;
    import com.efi.printsmith.events.StockOrderCommentsEvent;
    
	[Bindable]                                
    private var stockOrders:ArrayCollection = new ArrayCollection();
    [Bindable]                                
    private var stockOrdersDisplay:ArrayCollection = new ArrayCollection();
    [Bindable]
    private var mulitple:int;
    [Bindable]                                
    private var stockOrdersNonHistory:ArrayCollection = new ArrayCollection();      
    [Bindable]
    private var stockOrder:StockOrder;
    [Bindable]                                
    private var stockSelected:Boolean;
    [Bindable]
    private var webWorthy:Boolean;
   
   public function init():void {
		
		dataService.getAll("StockOrder");
		stockSelected = false;   
		webWorthy = false; 
	}


	public function getDateField(item:Object, column:DataGridColumn):String{
		var tmpItem:Date ;
		tmpItem = item.created as Date;
		
		if (tmpItem != null ) {
			return Snowmass.getShortDateTimeFormatter().format(tmpItem)
		} else {
			return "";
		}
	}   
	public function labelplaced(item:Object, column:DataGridColumn):String{
		if (item.placed ) {
			return "*"
		} else {
			return "";
		}
	
	}   
	public function labelfilled(item:Object, column:DataGridColumn):String{
		if (item.filled ) {
			return "*"
		} else {
			return "";
		}
	}   
   public function getVendorLabel(item:Object, column:DataGridColumn):String{
		var tmpItem:Vendor ;
		tmpItem = item.vendor as Vendor;
		
		if (tmpItem != null && tmpItem.name != null && tmpItem.name != "") {
			return tmpItem.name;
		} else {
			return "None";
		}
	}   
	
	private function handleSubmitOrderResult(event:ResultEvent):void {
		var result:Boolean = event.result as Boolean;
		var message:String;
		if (result == true) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'orderStockCmd.TheStockorderwasSUCC')
			Alert.show(message, "Success");    
		} else {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'orderStockCmd.Sorrytheordersubmiss')
			Alert.show(message, "Failed");
		}
		
	}
   
    private function handleLoadResult(event:ResultEvent):void {
    	stockOrders = new ArrayCollection();
    	stockOrdersDisplay = new ArrayCollection();
    	stockOrdersNonHistory = new ArrayCollection();
		stockOrders = event.result as ArrayCollection;
		if ( stockOrders != null)
		{
			stockOrdersDisplay = stockOrders;
			for each(var element:StockOrder in stockOrders) {
				if ( element.placed == false)
					stockOrdersNonHistory.addItem(element);
				}
			hideOrders();
		}
		
		
	}
	private function doStockLog():void {
		var myPrintJob:FlexPrintJob = new FlexPrintJob();
	
		if (myPrintJob.start()) {
			var myPrintDataGrid:PrintDataGrid = new PrintDataGrid();
	
			//Set the print view properties.
			myPrintDataGrid.width = myPrintJob.pageWidth;
			myPrintDataGrid.height = myPrintJob.pageHeight;
	
			// Set the data provider of the FormPrintView component's data grid
			// to be the data provider of the displayed data grid.
			myPrintDataGrid.dataProvider = dataGrid.dataProvider;
			myPrintDataGrid.columns = dataGrid.columns;
			myPrintDataGrid.visible = false;
			myPrintDataGrid.includeInLayout = false;
			
			Application.application.addChild(myPrintDataGrid);
	
			if(!myPrintDataGrid.validNextPage) {
				myPrintJob.addObject(myPrintDataGrid);
			} else {
				myPrintJob.addObject(myPrintDataGrid);
	
				while(true) {
					// Move the next page of data to the top of the print grid.
					myPrintDataGrid.nextPage();
	
					if(!myPrintDataGrid.validNextPage) {
						// This is the last page; queue it and exit the print loop.
						myPrintJob.addObject(myPrintDataGrid);
	
						break;
					} else {
						myPrintJob.addObject(myPrintDataGrid);
					}
				} 
	
			}
	
			Application.application.removeChild(myPrintDataGrid);
			myPrintJob.send();
	 	}
 	}
	private function doDelete():void{
	
	
		
		for each(var element:Object in dataGrid.selectedItems) {
			if( element != null)
				dataService.deleteItem("StockOrder", element.id);
			mulitple = mulitple + 1;
		}
		
		
		stockSelected= false;
		webWorthy = false;
		
	}
	
		
	
	private function doPartial():void{
		var setQuantity:QuantityUpdate = QuantityUpdate(PopUpManager.createPopUp(this, QuantityUpdate,true));
		setQuantity.setStyle("borderAlpha", 0.9);
		setQuantity.setQuantity((stockOrder.orderQuantity - stockOrder.receivedQuantity));
		setQuantity.addEventListener(QuantityEvent.Cancel, handleQuantityCancel);
	    setQuantity.addEventListener(QuantityEvent.OK, handleQuantityOK);
		
	}
	private function handleQuantityCancel(event:QuantityEvent):void{
		
	}
	private function handleQuantityOK(event:QuantityEvent):void{
		stockOrder.receivedQuantity = stockOrder.receivedQuantity + event.quantity;
		if (stockOrder.receivedQuantity == stockOrder.orderQuantity)
			stockOrder.filled = true
		dataService.addUpdate(stockOrder);
		dataGrid.selectedIndex = -1;
		dataService.addUpdate(stockOrder);
	}
	private function doRecieved():void{
		for each(var element:StockOrder in dataGrid.selectedItems) {
	
			if (element != null) {
				element.receivedQuantity = element.orderQuantity;
				element.filled = true;
				dataService.addUpdate(element);
			}
		}
						
		stockSelected = false;
		webWorthy = false;
		dataGrid.selectedIndex = -1;
		
		
	}
	private function doPlaced():void{
		for each(var element:StockOrder in dataGrid.selectedItems) {
			if( element != null){
				element.placed = true;
				dataService.addUpdate(element);
			}
		}
		
		stockSelected = false;
		webWorthy = false;
		dataGrid.selectedIndex = -1;
	}
	
	private function doSubmit():void {
		var stockOrderComments:StockOrderComments =  StockOrderComments(PopUpManager.createPopUp(this, StockOrderComments, true));
	    stockOrderComments.addEventListener(StockOrderCommentsEvent.OK, handleStockOrderComments);
	}
	private function handleStockOrderComments(ev:StockOrderCommentsEvent):void {
		xpedxIntegrationService.submitOrder(this.stockOrder);
	}
	private function handleSaveResult(ev:ResultEvent):void {
    	dataService.getAll("StockOrder");
	}
	private function handleDeleteResult(ev:ResultEvent):void {
	 mulitple = mulitple - 1;
	 if (mulitple == 0)
		dataService.getAll("StockOrder");
	}
	private function doStockGridClick():void {
	 	stockOrder = new StockOrder();
	 	stockOrder = dataGrid.selectedItem as StockOrder;
		stockSelected = true;
		if (stockOrder.vendor.name == "xpedx" || stockOrder.vendor.name == "Xpedx")
			webWorthy = true;
		else
			webWorthy = false;
		if ( stockOrder.placed)
			placed_button.enabled = false;
		else
			placed_button.enabled = true;
		
		if ( stockOrder.filled)
		{
			receiced_button.enabled= false;
			partial_button.enabled = false;
		}
		else{
			receiced_button.enabled= true;
			partial_button.enabled = true;
		}
	}
	
	private function handleFault(ev:FaultEvent):void {  
		var message:String;
		              
		message = "Error: "                          
		+ ev.fault.faultCode + " - "                                  
		+ ev.fault.faultDetail + " - "                                  
		+ ev.fault.faultString;
		Alert.show(message, "Error");   
	}	
		
	private function selectAll():void{
		var selectedIpodIndices:Array = [ ];
		stockSelected = true;
		for (var i:int = 0; i< stockOrdersDisplay.length ; i++)	{
			selectedIpodIndices.push(i);
		}
		dataGrid.selectedIndices =   selectedIpodIndices     
		                     
	}
	private function hideOrders():void{
		if (hide_previous.selected)
			stockOrdersDisplay = stockOrdersNonHistory;
		else
			stockOrdersDisplay = stockOrders;
	}
	private function dostockOrder():void{
		var importWindow:OrderStock = OrderStock(PopUpManager.createPopUp(this, OrderStock, true));
		importWindow.setStockOrder(dataGrid.selectedItem as StockOrder);
		PopUpManager.centerPopUp(importWindow);
	
	}
]]>
                             
</mx:Script>		
	<mx:Button x="24" y="25" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.SelectAll')}" click="selectAll()"/>
	<mx:Button x="171" y="25"  width="50" enabled="{stockSelected}" height="50"  id="delete_button" icon="@Embed(source='../../../../assets/delete48.png')" click="doDelete();"/>
	<mx:Button x="242" y="25" width="50" enabled="{stockSelected}" height="50" id="placed_button" icon="@Embed(source='../../../../assets/order.png')" click="doPlaced();"/>
	<mx:Button x="328" y="25" width="50" height="50" id="submit_button" icon="@Embed(source='../../../../assets/WebStatus.png')" click="doSubmit();" enabled="{webWorthy}"/>
	<mx:Button x="447" y="25" width="50"  enabled="{stockSelected}"  height="50" id="receiced_button" click="doRecieved()">
		<mx:icon>@Embed(source='../../../../assets/orderreceived.png')</mx:icon>
	</mx:Button>
	<mx:Button x="532" y="25" width="50" height="50" enabled="{stockSelected}" id="partial_button" click="doPartial();">
		<mx:icon>@Embed(source='../../../../assets/partialorderreceived.png')</mx:icon>
	</mx:Button>
	<mx:CheckBox x="610" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Printseparatepagefor')}" id="vendor_check" width="243.5"/>
	<mx:CheckBox x="610" y="39" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Hidepreviouslyordere')}" id="hide_previous" width="243.5" click="hideOrders()"/>
	<mx:Label x="171" y="83" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Delete')}" width="63"/>
	<mx:Label x="231" y="83" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.OrderPlaced')}" width="98"/>
	<mx:Label x="311" y="83" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.SubmitOrder')}" width="95"/>
	<mx:Label x="428" y="83" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.OrderReceived')}" width="106"/>
	<mx:Label x="528" y="83" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.PartialOrderReceived')}"/>
	<mx:DataGrid x="16.5" y="127"  allowMultipleSelection="true" doubleClick="dostockOrder()" height="231" width="837" dataProvider="{stockOrdersDisplay}" itemClick="doStockGridClick()" id="dataGrid">
		<mx:columns>  
			<mx:DataGridColumn  width="75" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Date')}" labelFunction="getDateField"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Invoice')}" dataField="invoiceNumber"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Job')}" dataField="jobNumber"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'wipRepCmd.CustomerName')}" dataField="customerName" width="150"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Vendor')}" labelFunction="getVendorLabel"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.StockDescription')}" dataField="name"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.StockNumber')}" dataField="vendorStockNumber"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'jobCmd.Parent')}" dataField="sheetSize"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Quantity')}" dataField="orderQuantity"/>
			
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Weight')}" dataField="weight"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Cost')}" dataField="cost"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Received')}" dataField="receivedQuantity"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Placed')}" labelFunction = "labelplaced"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'stockRepCmd.Filled')}" labelFunction = "labelfilled"/>
			
		</mx:columns>
	</mx:DataGrid>
	<mx:Button x="37" y="55"  height="38" width="38" icon="@Embed(source='../../../../assets/print.png')" click="doStockLog();"/>
	<mx:Label x="37" y="101" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Print')}"/>
	
</mx:TitleWindow>

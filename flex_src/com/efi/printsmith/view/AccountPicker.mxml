<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:vo="com.efi.printsmith.vo.*" width="630" height="612" defaultButton="{select_button}" showCloseButton="true" creationComplete="init()"
    close="cancelPicker();" xmlns:common="com.efi.printsmith.common.components.filter.*"
    xmlns:comp="com.efi.printsmith.common.components.*"
    title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'kCustPickerCommand.AccountPicker')}" xmlns:text="flash.text.*" borderThickness="3" borderStyle="solid">

<mx:RemoteObject id="dataService" destination="dataService">
	<mx:method name="getAccountPicker" fault="handleFault(event)" result="handleLoadResult(event)"/>
	<mx:method name="getContactPicker" fault="handleFault(event)" result="handleContactLoadResult(event)"/>
	<mx:method name="getAccountsByPartialName" fault="handleFault(event)" result="handleLoadResult(event)"/>
	<mx:method name="getById" fault="handleFault(event)" result="handleGetResult(event)"/>
</mx:RemoteObject>

<mx:Producer id="producer" destination="accounts"/>
<mx:Consumer id="consumer" destination="accounts" message="messageHandler(event.message)"/>
<mx:Consumer id="notification_consumer" destination="notifications" message="messageHandler(event.message)"/>

<mx:Script source="../common/scripts/CommonUtils.as" />

<mx:Script>
                                   
<![CDATA[
	import com.efi.printsmith.itemRenderers.PickerContactItemRenderer;
	import com.efi.printsmith.common.components.NonModalThrobberHelper;
	import com.efi.printsmith.components.Throbber;
	import com.efi.mdi.view.window.MDIWindow;
	import mx.collections.SortField;
	import mx.collections.Sort;
	import mx.events.DataGridEvent;
	import mx.controls.dataGridClasses.DataGridColumn;
	import com.efi.printsmith.common.components.filter.FilterProperty;
	import com.efi.printsmith.common.components.filter.FilterSearchBy;
	import mx.collections.ArrayCollection; 
	import com.efi.printsmith.events.AccountPickerSelectEvent;
	import com.efi.printsmith.data.Contact;           
    import com.efi.printsmith.data.Account;    
    import com.efi.printsmith.view.EditContact;
    import com.efi.printsmith.view.EditCustomer; 
    import com.efi.printsmith.view.StockDefinitions;           
	import mx.managers.PopUpManager;                                 
	import mx.controls.Alert;                          
	import mx.containers.Canvas;   
	import mx.core.Container;                      
	import mx.rpc.events.ResultEvent;                                 
	import mx.rpc.events.FaultEvent;                          
	import mx.events.ValidationResultEvent;                          
	import mx.validators.Validator;       
	import mx.messaging.messages.AsyncMessage;
	import mx.messaging.messages.IMessage;
	import mx.core.Application;
	                             
	[Bindable]                             
	private var message:String;                                   
	[Bindable]                             
	private var formIsValid:Boolean = false;                               
	
	[Bindable]                                     
	public var formIsEmpty:Boolean; 
	[Bindable]                                     
	public var prospect_boolean:Boolean; 
	 [Bindable]                                     
	public var contact_boolean:Boolean = true;                                  
	[Bindable]                                
	private var accounts:ArrayCollection = new ArrayCollection();
	
	private var ctAccounts:ArrayCollection = new ArrayCollection();
	private var acAccounts:ArrayCollection = new ArrayCollection();
	private var contactCallInProgress:Boolean;
	
	private var throbberHelper:NonModalThrobberHelper;
	                                                                          
	[Bindable]                               
	private var accountRecords:int = 0;
	         
	// Holds a reference to the currently focussed control on the form.
 	private var focusedFormControl:DisplayObject;                           
	
	private function searchAll():void	{
		if (chkSearchAll.selected)	{
			accounts.filterFunction = null; 
		}
		else	{
			accounts.filterFunction = accountFilter;
		}
		pickerFilter.clearFilter(true);
		refreshData();
	}           
	public function setAccounts( tempAccount:ArrayCollection):void{
	//	accountTypeRadio.selectedValue = "Customer"; 
		var i:int;
		this.accounts = tempAccount; //new ArrayCollection();
		
		acAccounts = cloneAC(tempAccount);
		
		
				
	}      
	              
	private function handleLoadResult(event:ResultEvent):void {
		accounts = event.result as ArrayCollection;
		accounts.filterFunction = accountFilter;
		refreshData();
	}
	
	private function handleFault(ev:FaultEvent):void {                               
		message = "Error: " + ev.fault.faultCode + " \n "                                            
		+ "Detail: " + ev.fault.faultDetail + " \n "                              
		+ "Message: " + ev.fault.faultString;                                    
	}

	private function messageHandler(message:IMessage):void
	{
		var messageBody:Object = message.body;// as com.efi.messaging.MessageBody;
		
		var messageType:String = messageBody.messageType;
		if (messageType == "ADDUPDATE" || messageType == "DELETE") {
			var payloadType:String = messageBody.payloadType as String; // What kind of item was added/deleted
			var payload:Number = messageBody.payload as Number; // ID if item added or deleted
			if (payloadType == "Account") {
				dataService.getAccountPicker();
			}
		}
	}
		                                                       
	private function init():void {   
		consumer.subscribe();
		notification_consumer.subscribe();   
		accountTypeRadio.selectedValue = "Customer";  
		prospect_radio.enabled = prospect_boolean;
		contact_radio.enabled = contact_boolean;
		//accounts.filterFunction = accountFilter;
		buildCustProspectList();
	    PopUpManager.centerPopUp(this);
//	    resetFocus();                                     
//		dataService.getAccountPicker();
		dataService.getContactPicker();
		
		contactCallInProgress = true;
		throbberHelper = new NonModalThrobberHelper(dataGrid,"throbber1");
		
		
		var editPage:Container = Snowmass.getInstance().containerManager.getActiveWindowContent();
		if (editPage.className != "EditCustomer") {
			new_button.visible = false;
			
		}
		refreshData();
	}
	
	private function accountFilter(item:Object):Boolean {
		var tempaccount:Account = item as Account;
		var showItem:Boolean = false;
		
		/* First make sure we have the right document type */
		switch (accountTypeRadio.selectedValue) {
		case "Customer":
			if (! tempaccount.prospect)
				showItem = true;
			break;
		case "Prospects":
			if (tempaccount.prospect)
				showItem = true;
			break;
		}
		
		return showItem
	}
	
	private function handleContactLoadResult(event:ResultEvent):void	{
		var contacts:ArrayCollection = event.result as ArrayCollection;
		var newAccounts:ArrayCollection = new ArrayCollection();
		if (contacts != null)	{
			for (var i:int = 0; i < contacts.length; i++)	{
				var c:Contact = contacts.getItemAt(i) as Contact;
				var a:Account = new Account();
					
				if (c.parentAccount == null)	{
					a.title = "Not Assigned";
				}
				else	{
					a.title = c.parentAccount.title;
					
					for (var j:int = 0; j<acAccounts.length; j++)	{
						var a1:Account = acAccounts.getItemAt(j) as Account;
						if (a1.id == c.parentAccount.id)	{
							a.prospect = a1.prospect;
							a.accountId = a1.accountId;
							break;
						}
					}
				}
				a.contact = c;
				newAccounts.addItem(a);
			
			}
		}
		ctAccounts = newAccounts;
		
		contactCallInProgress = false;
		if (throbberHelper.hideThrobberIfActive())	{
			accountTypeChange();
		}
	}
	
	private function accountTypeChange():void {
		pickerFilter.clearFilter(true);
		
		if (accountTypeRadio.selectedValue == "Contacts") {
			chkSearchAll.selected = false;
			chkSearchAll.visible = false;
			
			if (contactCallInProgress)	{
				throbberHelper.showThrobber();	
			}
			else	{
				buildContactList();	
				new_button.enabled = false;
			}
		}
		else	{
			chkSearchAll.selected = false;
			chkSearchAll.visible = true;
			
			throbberHelper.hideThrobberIfActive();
			
			buildCustProspectList();
			new_button.enabled = true;
		}
		refreshData();	
	}
	
	private function buildCustProspectList():void	{
		accounts = cloneAC(acAccounts);
		accounts.filterFunction = accountFilter;
		sortAccountsByTitle();
		dataGrid.dataField = "title";
		dataGrid.extLabelFunction = null;
		dataGrid.dgCol.itemRenderer = null;
		pickerFilter.searchCriteria = getSearchByForAccount();
 
	}  
	private function buildContactList():void	{
		accounts = cloneAC(ctAccounts);
		accounts.filterFunction = null;
		sortAccountsByLastName();
		dataGrid.dataField = null;
		dataGrid.extLabelFunction = contactLabel;
		dataGrid.dgCol.itemRenderer = new ClassFactory(PickerContactItemRenderer);
		pickerFilter.searchCriteria = getSearchByForContact();
		
	}
	
	private function sortAccountsByTitle():void	{
		var sortField:SortField = new SortField();
		sortField.name = "title";
		sortField.numeric = false;
		sortField.caseInsensitive = true;
		var titleSort:Sort = new Sort();
		titleSort.fields = [sortField];
		accounts.sort = titleSort;
		refreshData();	
	}
	private function sortAccountsByLastName():void	{
		var sortField:SortField = new SortField();
		sortField.compareFunction = compareByLastName;
		sortField.numeric = false;
		sortField.caseInsensitive = true;
		var nameSort:Sort = new Sort();
		nameSort.fields = [sortField];
		accounts.sort = nameSort;
		refreshData();	
	}
	
	private function compareByLastName(a:Object, b:Object):int{
		var acctA:Account = a as Account;
		var acctB:Account = b as Account;
		
		if (acctA.contact == null) return -1;
		if (acctB.contact == null) return -1;
		if (acctA.contact.lastName < acctB.contact.lastName)	{
			return -1;
		}
		else if (acctA.contact.lastName > acctB.contact.lastName)	{
			return 1;
		}
		else
			return 0;
	}
	public static function contactLabel(acct:Account):String	{
		if (acct.contact == null) return("");
		return acct.contact.lastName + ", " + acct.contact.firstName + " - " + acct.title;
	}
	
	private function refreshData():void{
		dataGrid.invalidateList();
		accounts.refresh();
	}                              
//	private function resetFocus():void {                           
//	     focusManager.setFocus(filter_field);                                    
//	}    
	
	private function editAccount():void {
		
	}   
	private function handleGetResult(ev:ResultEvent):void {
		var accountTemp:Account;
		accountTemp = ev.result as Account;
		var selectEvent:AccountPickerSelectEvent = new AccountPickerSelectEvent(AccountPickerSelectEvent.SELECTACCOUNT, accountTemp);
		dispatchEvent(selectEvent);
		PopUpManager.removePopUp(this);
	     
	}
	private function selectAccount():void {	
		var temp:Account;
		
		temp = dataGrid.selectedItem as Account;
		if (accountTypeRadio.selectedValue == "Contacts")	{
			temp = temp.contact.parentAccount;
			if (temp == null)	{
				var newEvent:AccountPickerSelectEvent = new AccountPickerSelectEvent(AccountPickerSelectEvent.SELECTUNASSIGNEDCONTACT, dataGrid.selectedItem as Account);
				dispatchEvent(newEvent);
				PopUpManager.removePopUp(this);
				return;
			}	
		}
			
		dataService.getById("Account", temp.id );
		
		
	}     
	
	private function newAccount():void {
		var newEvent:AccountPickerSelectEvent = new AccountPickerSelectEvent(AccountPickerSelectEvent.SELECTNEWACCOUNT, new Account());
		dispatchEvent(newEvent);
		PopUpManager.removePopUp(this);
	}
	
	private function cancelPicker():void {
		var cancelEvent:AccountPickerSelectEvent = new AccountPickerSelectEvent(AccountPickerSelectEvent.CANCELSELECTACCOUNT, null);
		dispatchEvent(cancelEvent);
		PopUpManager.removePopUp(this);
	}
	
//	private function updateDataGrid():void {
//		dataService.getAccountsByPartialName(filter_field.text);
//	}

	private function getSearchByForContact():ArrayCollection {
		var sc:ArrayCollection = new ArrayCollection();
		
		var fp:FilterProperty = new FilterProperty(new FilterProperty(null, "firstName"), "contact");
		var sc1:FilterSearchBy = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeExportsCmd.FirstName'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(new FilterProperty(null, "lastName"), "contact");
		var sc1:FilterSearchBy = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeExportsCmd.LastName'), fp);
		sc.addItem(sc1);
		
		fp = new FilterProperty(null, "accountId");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Acct'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(null, "externalRef");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Ref'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(new FilterProperty(null, "comLinks",true, "value","type","Phone"), "contact");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Phone'), fp);
		sc.addItem(sc1);
		
		fp = new FilterProperty(null, "masterAcct");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.MasterBilling'), fp);
		sc.addItem(sc1);
		//accounts.filterFunction = filterNullContacts;
		accounts.refresh();
		return sc;
		
	}
	private function getSearchByForAccount():ArrayCollection {
		var sc:ArrayCollection = new ArrayCollection();
		
		var fp:FilterProperty = new FilterProperty(null, "title");
		var sc1:FilterSearchBy = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Name'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(null, "accountId");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Acct'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(null, "externalRef");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Ref'), fp);
		sc.addItem(sc1);

		fp = new FilterProperty(new FilterProperty(null, "comLinks",true, "value","type","Phone"), "contact");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Phone'), fp);
		sc.addItem(sc1);
		
		fp = new FilterProperty(null, "masterAcct");
		sc1 = new FilterSearchBy(resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.MasterBilling'), fp);
		sc.addItem(sc1);
		//accounts.filterFunction = unfilterNullContacts;
		accounts.refresh();
		return sc;

	}
	public function filterNullContacts(item:com.efi.printsmith.data.Account):Boolean {
		return item.contact != null;
	}
	
	public function unfilterNullContacts(item:com.efi.printsmith.data.Account):Boolean {
		return true;
	}
]]>        
</mx:Script>
                             

                             
	<mx:Canvas width="100%" height="100%">
		<comp:SingleColumnDataGrid id="dataGrid" width="457" height="420" dataProvider="{accounts}"
		doubleClickEnabled="true" doubleClick="selectAccount()"  showHeaders="false" backgroundColor="#FFFFFF" borderStyle="solid" borderThickness="3" y="65"               
		dataField="title" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'kContactManagerCommand.Name')}"                          
		/>
		<mx:Button label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Select')}" id="select_button" enabled="{(dataGrid.selectedItem != null)}" click="selectAccount();"  width="108" x="471" y="65"/>
		<mx:Button label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rStandardMenuText.New')}" enabled="true" click="newAccount();" id="new_button" width="108" x="471" y="95"/>
		<mx:Button label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Cancel')}" click="cancelPicker();" id="cancel_button" width="108" x="471" y="150"/>
		<common:PickerFilter id="pickerFilter" x="10" y="495" width="417" showExtendedSearch="true" height="26" searchComponent="{dataGrid}" listData="{accounts}" searchCriteria="{getSearchByForAccount()}" />
		<!--<mx:TextInput x="247" y="493" id="filter_field" width="210" keyUp="updateDataGrid()"/>
		<mx:ComboBox x="55" y="493" id="filter_combobox" width="184">
			<mx:ArrayCollection>
				<mx:String>{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Name')}</mx:String>
				<mx:String>{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Acct')}</mx:String>
				<mx:String>{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Ref')}</mx:String>
				<mx:String>{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Phone')}</mx:String>
				<mx:String>{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.MasterBilling')}</mx:String>
			</mx:ArrayCollection>
		</mx:ComboBox>
		<mx:Label x="10" y="495" text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'labelCmd.Filter')}"/>-->
		<mx:RadioButtonGroup id="accountTypeRadio" change="accountTypeChange()"/>
		<mx:RadioButton x="49" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Customers')}" groupName="accountTypeRadio" value="Customer" id="customer_radio"/>
		<mx:RadioButton x="190" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Prospects')}" groupName="accountTypeRadio" value="Prospects" id="prospect_radio"/>
		<mx:RadioButton x="300" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Contacts')}" groupName="accountTypeRadio" value="Contacts" id="contact_radio"/>
		<mx:CheckBox label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'listerCmd.Searchallaccounts')}" id="chkSearchAll" click="searchAll();"  x="131" y="520"/>
		</mx:Canvas>
<mx:Spacer height="10" />        

</mx:TitleWindow>
<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow 
	xmlns:comp="com.efi.printsmith.components.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:vo="com.efi.printsmith.vo.*" 
	label="Credit Card Approval" name="Credit Card Approval"
width="100%" height="100%"  creationComplete="init()" 
	 showCloseButton="true" 
     title="Credit Card Approval" xmlns:text="flash.text.*" xmlns:ns1="com.efi.printsmith.components.*" close="doClose()">
	<mx:RemoteObject id="dataService"
					 destination="dataService">
					 
		<mx:method name="getAll"
				   fault="handleFault(event)"
				   result="handleLoadResult(event)"/>
				   
		<mx:method name="addUpdate" fault="handleFault(event)" result="handleSaveResult(event)"/>
		
		<mx:method name="getCCTbyReferenceNumber" 
				fault="handleFault(event)" 
				result="handleGetByRefResult(event)"/>
				
	</mx:RemoteObject>
	
	<mx:RemoteObject id="creditCardService"
					 destination="creditCardService">
		<mx:method name="sendCardTransaction"
				   fault="handleFault(event)"
				   result="handleSendCardTransResult(event)"/>
	</mx:RemoteObject>

<mx:Script source="../common/scripts/StringUtils.as"/>
	
<mx:Script>                                   
<![CDATA[
	import com.efi.printsmith.data.Address;
	import com.efi.printsmith.data.Contact;
	import com.efi.printsmith.events.CreditCardTrackReadEvent;
	import com.efi.printsmith.data.CreditCard;
	import com.efi.printsmith.data.CreditCardTransactions;
	import com.efi.printsmith.data.PreferencesCreditCard;
	import com.efi.printsmith.events.EncryptCCTransEvent;
	
	import com.efi.printsmith.events.CreditCardTrackReadEvent;
	import com.efi.printsmith.events.CreditCardTransEvent;
	import mx.rpc.events.ResultEvent;
	import mx.collections.ArrayCollection;
	import mx.rpc.events.ResultEvent;
	import mx.rpc.events.FaultEvent;
	import mx.controls.Alert;
	import mx.managers.PopUpManager;
	import mx.rpc.IResponder;
	import mx.utils.StringUtil;
	import mx.controls.DateField;
	
	import com.efi.printsmith.Constants;
		
	[Bindable]	
		private var application:Snowmass;
	[Bindable]
		private var localTransation:CreditCardTransactions;
	
	[Bindable] public static var preferencesCreditCardArray:ArrayCollection = new ArrayCollection();
	
	private var currencyValidator:CurrencyValidator = Snowmass.getCurrencyValidator();
	private var numberHandEntered:Boolean = false;
	
    public function init():void{		
   		application= Snowmass.getInstance();
     	Snowmass.getInstance().addEventListener(CreditCardTrackReadEvent.BRICKREAD, handleBrickRead);
  	   	localTransation = new CreditCardTransactions;
   		localTransation.creditCard = new CreditCard;
   		localTransation.verbalAuthDate = null;			// dont have any date
 		
 		this.throbber.visible = false;
		this.throbber.stop();

 		// start out as a charge
 		localTransation.transactionType = Constants.CREDIT_CARD_TRANSACTION_TYPE_kTransactionTypePurchase;
 		
  		dataService.getAll("PreferencesCreditCard");
		
		if (application.currentUser.robustPassword == false) {
			cardnumber.editable = false;
			cardnumber.alpha = .5;
			cardholder.editable = false;
			cardholder.alpha = .5;
		}
	}
	
	private function handleSendCardTransResult(ev:ResultEvent):void
	{
		if (ev.result != null) {
			var results:Number = ev.result as Number;
			var selectEvent:CreditCardTransEvent;
			
			if (results == localTransation.id) {
				//
				// update other open windows
				//
				selectEvent = new CreditCardTransEvent(CreditCardTransEvent.COMPLETE, localTransation);
				dispatchEvent(selectEvent);
		
				//
				//
				//
				this.throbber.visible = false;
				this.throbber.stop();
				
				// close this window TEMP
				PopUpManager.removePopUp(this);
			}
		}
	}


	private function handleBrickRead(ev:CreditCardTrackReadEvent):void 
	{
		var valid:Boolean;
		var returnVal:Array = new Array;
		
		localTransation.creditCard.cardNumber = ev.brickData.parsedCard.cardNumber;
		returnVal = validCreditCard(localTransation.creditCard);
		
		valid = returnVal[0] as Boolean;
		cardstatus.text = returnVal[1] as String;
		localTransation.creditCard.type = returnVal[2] as String;

		localTransation.creditCard.cardDisplayNumber = formatCreditCardNumber(ev.brickData.parsedCard.cardNumber);
		localTransation.creditCard.cardHolderName = ev.brickData.parsedCard.cardHolderName;
		localTransation.creditCard.expiresDate = ev.brickData.parsedCard.expiresDate;
		
		if (valid == false) {
			okButton.enabled = false;
		} else {
			okButton.enabled = true;
		}
	}

	private function handleFault(ev:FaultEvent):void
	{
		var message:String;

		message="Error: " + ev.fault.faultCode + " - " + ev.fault.faultDetail + " - " + ev.fault.faultString;
		Alert.show(message, "Error", 0, null);
	}
	private function handleLoadResult(ev:ResultEvent):void{
		preferencesCreditCardArray = ev.result as ArrayCollection;
	}
	private function doClose():void{
		PopUpManager.removePopUp(this);
	}
	
	
	private function handleGetByRefResult(ev:ResultEvent):void{
		var temp:CreditCardTransactions = ev.result as CreditCardTransactions;
		
		if (temp != null) {
			localTransation.creditCard = temp.creditCard;
			localTransation.amount = temp.amount;
			localTransation.tax = temp.tax;
			
			doReadCardNumber();		// handle status update
		}
	}
		
	private function handleSaveResult(ev:ResultEvent):void{
		var temp:CreditCardTransactions = ev.result as CreditCardTransactions;
		var selectEvent:CreditCardTransEvent;
		
		if (temp != null) {
			//
			// update the local copy to reflect the new saved changes, (id)
			//
			localTransation = temp;
			
			//
			// log an event to the fact a credit card number was seen, if done.
			//
			if (numberHandEntered) {
				Snowmass.getInstance().logUserEvent(Constants.USER_EVENT_CREDITCARD_TAKEN, null, null);
			}
	
			//
			// update other open windows
			//
			selectEvent = new CreditCardTransEvent(CreditCardTransEvent.WRITE, temp);
			dispatchEvent(selectEvent);
			
			//
			// use the ID from record now
			// start process to charge card
			creditCardService.sendCardTransaction(temp.id);
		}
	}
	
	private function doOK():void{
		this.throbber.visible = true;
		this.throbber.play();
		
		okButton.enabled = false;
		cancelButton.enabled = false;
		
		localTransation.amount = parseCurrencyFloat(amount.text);
		localTransation.poNumber = ponumber.text;
		localTransation.verbalAuthCode = verbAuthNumber.text;
		
		if (VerbAuthDate.text.length > 0)
			localTransation.verbalAuthDate = DateField.stringToDate(VerbAuthDate.text, "MM/DD/YYYY");
		else
			localTransation.verbalAuthDate = null;
			
		localTransation.tax = parseCurrencyFloat(tax.text);
		localTransation.orderNumber = ordernumber.text;
		localTransation.poNumber = ponumber.text;
		localTransation.tempCVV2 = cardverification.text;
		localTransation.creditCard.address1 = street.text;
		localTransation.creditCard.zipCode = zip.text;

		//
		// follow the chain of events.
		// encypt
		// send events
		// close window
		// kick start processing of transaction
		doEncryptCardData();
	}
	
	//
	// After a Encryption, finish setup of the transaction and kick off
	//
	private function handleEncryptionResult(event:ResultEvent):void {
		var cct:CreditCardTransactions;
		
		if (event.result != null) {
			cct = event.result as CreditCardTransactions;
			
			// setup and ready for process now
			localTransation.creditCard.encryption = "3";
			localTransation.transactionStatus = Constants.CREDIT_CARD_TRANSACTION_STATUS_kTransactionStatusWaiting;
			localTransation.isCurrentTransaction = 1;

			if (Snowmass.getInstance().currentUser != null) {
				localTransation.userName = Snowmass.getInstance().currentUser.name;
			}
			
			//
			// bring over the items effected by encryption
			//
			localTransation.creditCard.cardNumber = cct.creditCard.cardNumber;
			localTransation.trackOne = cct.trackOne;
			localTransation.trackTwo = cct.trackTwo;
			localTransation.tempCVV2 = cct.tempCVV2;
			
			// save data
			// see "handleSaveResult" for next processing step
			//
			dataService.addUpdate(localTransation);
		}
	}

	//****************************************************************************
	// IsCardNumberModeTen - This is a simple mod 10 of the credit card number to insure its a valid card.
	//						See "Luhun Algorithm"
	//
	//	Test numbers:  6011222233334444
	//				:  4000000000000002
	//****************************************************************************
	public static function IsCardNumberModeTen(cardNumber:String):Boolean
	{
		var calcs:int;
		var CalcCard:int = 0;
		var i:int;
		var temp:int;
		var flag:Number = 0;
		var byteVar:ByteArray = new ByteArray;
		var xPattern:int;
		
		//
		// None can be shorter than 13 digits long
		//
		if (cardNumber.length < 13)
			return(false);
		
		xPattern = cardNumber.indexOf("X");
		// is the number already formatted
		if (xPattern < 0) {
			byteVar.writeUTFBytes(cardNumber);
			
			//
			// start at the end and convert each digit into a number and total then together, starting on the second
			//	digit from the right multiply it by 2 and do the same for every other digit.
			//
			for (i = (byteVar.length-1); i >= 0; --i)
			{
				temp = byteVar[i];
				if (flag == 0)
				{
					calcs = (temp - 48);
					flag = 1;
				}
				else
				{
					calcs = ((temp - 48) * 2);
					flag = 0;
				}
				
				if (calcs > 9)
				{
					calcs -= 9;
				}
				
				CalcCard += calcs;
			}
			
			//
			// The MOD 10 of the total should be evenly divisible, if not then a bad digit was found.
			//
			if ((CalcCard%10) == 0) return(true);
		
			return(false);
		} else
			return(true);
			
	} // IsCardNumberModeTen
		
	private function validCreditCard(testCC:CreditCard):Array {
		var results:Boolean = false;
		var cardType:String = new String;
		var cardTitle:String = new String;
		var message:String = new String;
		var	index:Number;
		var cardTypeValid:Boolean = false;
		var localCCPref:PreferencesCreditCard = new PreferencesCreditCard;
		var today:Date = new Date;
		
		if (testCC.cardNumber.length > 0) {
			cardType = testCC.cardNumber.charAt(0);
		}
				
		if (preferencesCreditCardArray.length > 0) {
			for (index = 0; index < preferencesCreditCardArray.length; ++index) {
				localCCPref = preferencesCreditCardArray.getItemAt(index) as PreferencesCreditCard;
				if (localCCPref != null && localCCPref.activeAccepted == true) {
					if (localCCPref.firstNumberPrefix.toString() == cardType) {
						cardTypeValid = true;
						cardTitle = localCCPref.cardType;
					}
				}
			}
		}
		
		if (testCC.cardNumber.length < 13 || testCC.cardNumber.length > 19) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.NotValidNumber');
		} else if (cardTypeValid == false) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Cardtypenotaccepted');
		} else if (testCC.cardHolderName.length == 0) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Needcardholdername');
		} else if (testCC.expiresDate == null || testCC.expiresDate.valueOf() == 0) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Needsexpirationdate');
		} else if (testCC.expiresDate < today) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Cardisexpired');
		} else if (IsCardNumberModeTen(testCC.cardNumber) == false) {
			message = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.NotValidNumber');
		} else {
			message = cardTitle;
			results = true;
		}
		
		return [results, message, cardType];
	}
	
	//
	// encrypt all fields at one time
	//	Card Number
	//	Track Data
	//	CVV2
	// 
	private function doEncryptCardData():void{
		var callbacks:IResponder = new mx.rpc.Responder(handleEncryptionResult, handleFault);	
		var encryptCCEvent:EncryptCCTransEvent = new EncryptCCTransEvent(EncryptCCTransEvent.ENCRYPTCCTRANS, localTransation, callbacks);
		encryptCCEvent.dispatch();
	}
	
	private function formatCreditCardNumber(str:String):String {
		var localString:String = new String;
		var xPattern:int;
		
		if (str != "") {
			xPattern = str.indexOf("X");
			
			if (xPattern < 0) {
				var i:Number;
				for (i = 0; i < str.length; ++i) {
					if (i > 0 && i < (str.length-4)) {
						localString = localString.concat("X");
					}
					else {
						localString = localString.concat(str.charAt(i).toString());
					}
				}
				
				return(localString);
			}
		} else {
			localTransation.creditCard.cardNumber = "";
			localTransation.creditCard.encryption = "";
		}
		return(str);
	}
	
	//
	// lookup a transaction based on the reference number
	//
	private function doReadReferenceNumber():void{
		dataService.getCCTbyReferenceNumber(refundRefNumber.text);
	}
	
	private function doReadCardNumber():void{
		var valid:Boolean;
		var returnVal:Array = new Array;
		
		cardnumber.text = StringUtil.trim(cardnumber.text);
		localTransation.creditCard.cardNumber = cardnumber.text;
		returnVal = validCreditCard(localTransation.creditCard);
		
		valid = returnVal[0] as Boolean;
		cardstatus.text = returnVal[1] as String;
		localTransation.creditCard.type = returnVal[2] as String;
		
		// preference lookup worked, so format as displayed
		if (localTransation.creditCard.type != "") {
			cardnumber.text = formatCreditCardNumber(cardnumber.text);
			localTransation.creditCard.cardDisplayNumber = cardnumber.text;
		}

		if (valid == false) {
			okButton.enabled = false;
		} else {
			okButton.enabled = true;
		}
		
		numberHandEntered = true;		// user can seen a credit card number
	}
	
	private function doReadCardHolder():void{
		var valid:Boolean;
		var returnVal:Array = new Array;
		
		localTransation.creditCard.cardHolderName = cardholder.text;
		returnVal = validCreditCard(localTransation.creditCard);
		
		valid = returnVal[0] as Boolean;
		cardstatus.text = returnVal[1] as String;
		
		if (valid == false) {
			okButton.enabled = false;
		} else {
			okButton.enabled = true;
		}
	}
		
	private function doReadExpirationDate(event:Event):void {
		var valid:Boolean;
		var returnVal:Array = new Array;

		var dateObj:Date = event.currentTarget.selectedDate as Date;
		
		if (dateObj != null) {
			localTransation.creditCard.expiresDate = dateObj;
			
			returnVal = validCreditCard(localTransation.creditCard);
			
			valid = returnVal[0] as Boolean;
			cardstatus.text = returnVal[1] as String;
			
			if (valid == false) {
				okButton.enabled = false;
			} else {
				okButton.enabled = true;
			}
		}
	}
	
	private function doChargeType():void {
		localTransation.transactionType = Constants.CREDIT_CARD_TRANSACTION_TYPE_kTransactionTypePurchase;
		ponumber.enabled = true;
		cardverification.enabled = true;
		street.enabled = true;
		zip.enabled = true;
		refundRefNumber.setStyle('backgroundColor',0xFFFFFF);
	}
	
	private function doRefundType():void {
		localTransation.transactionType = Constants.CREDIT_CARD_TRANSACTION_TYPE_kTransactionTypeCredit;
		ponumber.enabled = false;
		cardverification.enabled = false;
		street.enabled = false;
		zip.enabled = false;
		
		refundRefNumber.setFocus();
		refundRefNumber.setStyle('backgroundColor',0x8BE4FF);		// light blue
	}

    private function dateField_labelFunc(item:Date):String {
        if (item == null) {
            return "";
        }
        return formatDate.format(item);
    }
	
]]>
                             
</mx:Script>
   	<mx:CurrencyValidator source="{amount}" property="text" trigger="{amount}" triggerEvent="focusOut" allowNegative="true"/>
	<mx:DateFormatter id="formatDate" formatString="MM/DD/YYYY" />

	<mx:Canvas width="100%" height="100%">
		
		<mx:TextInput x="197" y="69" width="228" id="cardnumber"  text="{localTransation.creditCard.cardDisplayNumber}"  focusOut="doReadCardNumber()"/>
		<mx:Label x="10"
				  y="72"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.CardNumber')}"
				  fontSize="9" textAlign="right" width="179" id="label2"/>
		<mx:TextInput x="197" y="97" width="228" id="cardstatus" enabled="false"/>
		<mx:Label x="10"
				  y="100"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.CardStatus')}"
				  fontSize="9" textAlign="right" width="179" id="label1"/>
		<mx:TextInput x="197" y="125" width="228" id="cardholder" text="{localTransation.creditCard.cardHolderName}" focusOut="doReadCardHolder()"/>
		<mx:Label x="10"
				  y="128"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.CardHolder')}"
				  fontSize="9" textAlign="right" width="179" id="label0"/>
		<mx:DateField x="197" y="153" width="170" id="expires"  selectedDate="{localTransation.creditCard.expiresDate}" 
				labelFunction="dateField_labelFunc" formatString="MM/DD/YYYY" 
				text="{localTransation.creditCard.expiresDate}" 
				change="{doReadExpirationDate(event)}"/>
		<mx:Label x="10"
				  y="156"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Expires')}"
				  fontSize="9" textAlign="right" width="179" id="label20"/>
		<mx:TextInput restrict="0-9\." x="197" y="181" width="170" id="amount" text="{Snowmass.getCurrencyFormatter(false, 2).format(localTransation.amount)}"/>
		<mx:Label x="10"
				  y="184"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Amount')}"
				  fontSize="9" textAlign="right" width="179" id="label3"/>
		<mx:TextInput restrict="0-9\." x="197" y="209" width="170" id="tax" text="{Snowmass.getCurrencyFormatter(false, 2).format(localTransation.tax)}"/>
		<mx:Label x="10"
				  y="212"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Tax')}"
				  fontSize="9" textAlign="right" width="179" id="label4"/>
		<mx:TextInput x="197" y="237" width="170" id="ordernumber" text="{localTransation.orderNumber}"/>
		<mx:Label x="10"
				  y="240"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.OrderNumber')}"
				  fontSize="9" textAlign="right" width="179" id="label5"/>
		<mx:TextInput x="197" y="265" width="170" id="ponumber" text="{localTransation.poNumber}"/>
		<mx:Label x="10"
				  y="268"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.PONumber')}"
				  fontSize="9" textAlign="right" width="179" id="label6"/>
		
		<mx:TextInput x="197" y="293" width="170" id="cardverification" maxChars="4" text="{localTransation.tempCVV2}"/>
		<mx:Label x="10"
				  y="296"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.CardVerification')}"
				  fontSize="9" textAlign="right" width="179" id="label7"/>
		<mx:Label x="375" y="295" text="CVV2/CID" width="69"/>
		
		<mx:TextInput x="197" y="323" width="228" id="street" text="{localTransation.creditCard.address1}"/>
		<mx:Label x="10"
				  y="326"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Street')}"
				  fontSize="9" textAlign="right" width="179" id="label9"/>
		<mx:TextInput x="197" y="351" width="228" id="zip" text="{localTransation.creditCard.zipCode}"/>		
		<mx:Label x="10"
				  y="354"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Zip')}"
				  fontSize="9" textAlign="right" width="179" id="label12"/>
				  
		<mx:TextInput x="197" y="406" width="170" id="refundRefNumber" text ="{localTransation.referenceNumber}" focusOut="doReadReferenceNumber()"/>
		<mx:Label x="10"
				  y="409"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.RefundReference')}"
				  fontSize="9" textAlign="right" width="179" id="label13"/>
			  
		<mx:TextInput x="197" y="436" width="170" id="verbAuthNumber" text="{localTransation.verbalAuthCode}"/>
		<mx:Label x="10"
				  y="439"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.VerbalAuthNumber')}"
				  fontSize="9" textAlign="right" width="179" id="label10"/>	
		<mx:TextInput x="197" y="466" width="170" id="VerbAuthDate" text="{localTransation.verbalAuthDate}"/>
		<mx:Label x="10"
				  y="469"
				  text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.VerbalAuthDate')}"
				  fontSize="9" textAlign="right" width="179" id="label11"/>		
		
		<mx:Button x="10" y="542" label="OK" width="70" id="okButton" click="doOK()"/>
		<mx:Button x="364" y="542" label="Cancel" width="70" id="cancelButton" click="doClose()"/>
		
		<comp:SmallThrobber id="throbber" height="20" width="20" visible="true" x="220" y="542"/>
		
		<mx:Canvas x="10" y="10" width="514" height="42" id="ChargeRefundArea" borderStyle="solid" borderColor="#518AA2" cornerRadius="6" borderThickness="2">
			<mx:RadioButtonGroup id="ChargeRefund"/>
			<mx:RadioButton x="29" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Charge')}" groupName="ChargeRefund" width="181" selected="true" change="doChargeType()"/>
			<mx:RadioButton x="336" y="10" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'creditCardManAppvCmd.Refund')}" groupName="ChargeRefund" width="143" change="doRefundType()"/>
		</mx:Canvas>
		
	</mx:Canvas>	
</mx:TitleWindow>

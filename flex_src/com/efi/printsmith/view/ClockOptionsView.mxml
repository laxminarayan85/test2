<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="vertical" width="100%" height="100%" showCloseButton="true"
	headerHeight="0" title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rStandardMenuText.ClockOptions')}"
	close="{closeWindow()}" creationComplete="{initClockOptsWindow()}" xmlns:components="com.efi.printsmith.common.components.*"
	implements="com.efi.mdi.IMDIWindowAware">

	<mx:Script source="../common/scripts/CommonUtils.as"/>
		
	<mx:Script>
		<![CDATA[
			import mx.utils.StringUtil;
			import com.efi.mdi.view.window.MDIWindow;
			import com.efi.mdi.event.window.WindowSaveSuccessEvent;
			import com.efi.mdi.event.compare.MDICompareObjectEvent;
			import mx.utils.ObjectUtil;
			import com.efi.printsmith.data.TimeCard;
			import com.efi.printsmith.model.AutoClockOutDetector;
			import mx.controls.Alert;
			import mx.rpc.events.ResultEvent;
			import mx.collections.ArrayCollection;
			import mx.rpc.events.FaultEvent;
			import mx.managers.PopUpManager;
			import com.efi.printsmith.data.Employee;
			
			[Bindable] public var employeeList:ArrayCollection = new ArrayCollection();
			
			[Bindable] public var employee:Employee;
			
			[Bindable] public var originalEmployee:Employee;
			
			[Bindable] public var autoClockOutDetectortList:ArrayCollection = new ArrayCollection();
			
			[Bindable] public var requestCount:int=0;
			
			[Bindable] public var checkCount:int=0;
			
			private function initClockOptsWindow():void {
				PopUpManager.centerPopUp(this);
				var mdiWin:MDIWindow = Snowmass.getInstance().containerManager.getWindowWithChild(this);
				mdiWin.title = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rStandardMenuText.ClockOptions');
				dataService.getAll("Employee");
			}
			
			private function setEmployeeObj(event:Event):void {
				var inputArguments:Array = new Array();
				inputArguments.push(event.currentTarget.selectedIndex);
				inputArguments.push(event.currentTarget.selectedItem);
				var mDICompareObjectEvent:MDICompareObjectEvent = new MDICompareObjectEvent(MDICompareObjectEvent.MDI_COMPAREOBJECT,this,doSelectOperation,inputArguments);
				dispatchEvent(mDICompareObjectEvent);
			}
			
			private function doSelectOperation(index:int, item:Object):void {
				if(index>-1 && item!=null){
					employee = item as Employee;
				} else {
					employee = null;
				}
				setOriginalEmployee();
			}
			
			private function handleLoadResult(event:ResultEvent):void {
				employeeList = event.result as ArrayCollection;
				for each(var employeeObj:Employee in employeeList){
					employeeObj.autoOutTime = employeeObj.autoHour+":"+employeeObj.autoMin;
				}
				if(employee!=null){
					var counter:int=0;
					for each(var employeeObject:Employee in employeeList){
						if(employeeObject.id == employee.id) {
							employeeDG.selectedIndex = counter;
							employee = employeeObject;
							break;
						}
						counter++;
					}
				}
				setOriginalEmployee();
			}
			
			private function handleFault(ev:FaultEvent):void {  
				var message:String;
				              
				message = "Error: "                          
				+ ev.fault.faultCode + " - "                                  
				+ ev.fault.faultDetail + " - "                                  
				+ ev.fault.faultString;
				Alert.show(message, "Error", 0, null, null, null, 4);                                 
			}
			
			private function setAutoHours(event:Event):void {
				if(employee!=null){
					if(employee.autoOutTime!=null && employee.autoOutTime!=""){
						var hours:Number = Number(employee.autoOutTime.split(":")[0]);
						var minutes:Number = Number(employee.autoOutTime.split(":")[1]);
						employee.autoHour = hours;
						employee.autoMin = minutes;
					} else {
						employee.autoHour = 0;
						employee.autoMin = 0;
					}
				}
			}
			
			private function getAutoOutText(timeStr:String):String {
				if(timeStr!=null && timeStr!=""){
					var am_pm_text:String = "AM";
					var hours:Number = Number(timeStr.split(":")[0]);
					var minutes:Number = Number(timeStr.split(":")[1]);
					if(hours==0 && minutes==0){
						return "";
					} else {
						if(hours>11 && hours<24){
							am_pm_text = "PM";
						}
						if(hours>12){
							hours = hours-12;
						} else if(hours==0){
							hours = 12;
						}
						if(String(minutes).length>1){
							return hours+":"+minutes+" "+am_pm_text;
						} else {
							return hours+":"+"0"+minutes+" "+am_pm_text;
						}
					}
				}
				return "";
			}
			
			private function doSave():void {
				if(employee!=null){
					dataService.addUpdate(employee);
				}
			}
			
			private function triggerAutoClockoutDetector(autoClockOutEmployeeList:ArrayCollection):void {
				for each(var employeeObj:Employee in autoClockOutEmployeeList){
					dataServiceTimeCard.getByClockInOut("TimeCard",employeeObj);
				}
			}
			
			private function handleLoadTimeCardResult(ev:ResultEvent):void {
				checkCount++;
				var timecardArray:ArrayCollection = ev.result as ArrayCollection;
				var autoClockOutDetector:AutoClockOutDetector = new AutoClockOutDetector();
				autoClockOutDetector.employee = employee;
				autoClockOutDetector.timeCard =	timecardArray.getItemAt(0,0) as TimeCard; 
				autoClockOutDetector.start();
				autoClockOutDetectortList.addItem(autoClockOutDetector);
				/* if(requestCount==checkCount){
					PopUpManager.removePopUp(this);
				} */
			}
			
			private function handleSaveResult(event:ResultEvent):void {
				openConfirmationComponent(StringUtil.substitute(resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'CommonConfirmation.SAVE'),resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'workflowCmd.Employee')));
				this.employee = event.result as Employee;
				setOriginalEmployee();
				var requestSent:Boolean = false;
				var autoClockOutEmployeeList:ArrayCollection = new ArrayCollection();
				for each(var employeeObj:Employee in employeeList){
					autoClockOutDetectortList = Snowmass.getInstance().autoClockOutDetectortList;
					var count:int=-1;
					var checkFlag:Boolean = false;
					for each(var autoClockOutDetectorObj:AutoClockOutDetector in autoClockOutDetectortList){
						count++;
						if(autoClockOutDetectorObj.employee.id==employeeObj.id){
							checkFlag = true;
							break;
						}
					}
					if(count!=-1 && checkFlag){
						var autoClockOutDetectorObject:AutoClockOutDetector = autoClockOutDetectortList.getItemAt(count) as AutoClockOutDetector;
						if(employeeObj.autoHour==0 && employeeObj.autoMin==0){
							autoClockOutDetectorObject.stop();
							autoClockOutDetectortList.removeItemAt(count);
						} else {
							if(autoClockOutDetectorObject.employee.autoHour!=employeeObj.autoHour || autoClockOutDetectorObject.employee.autoMin!=employeeObj.autoMin){
								autoClockOutDetectorObject.employee = employeeObj;
							}
						}
					} else {
						if(employeeObj.clockIn || employeeObj.clockBreak){
							if(employeeObj.autoHour!=0 || employeeObj.autoMin!=0){
								requestSent = true;
								requestCount++; 
								autoClockOutEmployeeList.addItem(employeeObj);
							}
						}
					}
				}
				if(requestSent){
					triggerAutoClockoutDetector(autoClockOutEmployeeList);
				}
				dispatchEvent(new WindowSaveSuccessEvent(WindowSaveSuccessEvent.WINDOW_SAVESUCCESS_STATUS,true));
				/* if(!requestSent){
					PopUpManager.removePopUp(this);
				} */
			}
			
			private function doRevert():void {
				dataService.getAll("Employee");
			}
			
			private function closeWindow():void{
				Snowmass.getInstance().containerManager.getWindowWithChild(this).closeWindow();
			}
			
			public function setOriginalEmployee():void {
				registerClassAlias("com.efi.printsmith.data.Employee",Employee);
				originalEmployee = ObjectUtil.copy(employee) as Employee;
			}
			
			public function getOriginalObject():ArrayCollection {
				var originalList:ArrayCollection = new ArrayCollection();
				originalList.addItem(originalEmployee);
				return originalList;
			}
				
			public function getUpdatedObject():ArrayCollection {
				var updatedList:ArrayCollection = new ArrayCollection();
				updatedList.addItem(employee);
				return updatedList;
			}
			
			public function getConfirmationText():String {
				if(employee!=null){
					return "\n"+employee.lastName;
				}
				return "";
			} 
				
			public function onClose():void {
				
			}
			
			public function onSave():void {
				doSave();
			}
			
			public function onDontSave():void {
				dataService.getAll("Employee");
			}
			
			public function onCancel():void {
				if(employee!=null && employeeList!=null && employeeList.length>0){
					var counter:int=0;
					for each(var employeeObj:Employee in employeeList) {
						if(employeeObj.id == employee.id){
							employeeDG.selectedItem = employeeObj;
							employeeDG.selectedIndex = counter;
							break;	
						}
						counter++;
					}
				}
			}
		]]>
	</mx:Script>
	
	<mx:RemoteObject id="dataService" destination="dataService" showBusyCursor="true">
		<mx:method name="getAll" fault="handleFault(event)" result="handleLoadResult(event)"/>
		<mx:method name="addUpdate" fault="handleFault(event)" result="handleSaveResult(event)"/>
	</mx:RemoteObject>
	
	<mx:RemoteObject id="dataServiceTimeCard" destination="dataService" showBusyCursor="true">
		<mx:method name="getByClockInOut" fault="handleFault(event)" result="handleLoadTimeCardResult(event)"/>
	</mx:RemoteObject>
	
	<mx:HBox width="100%" height="55%">
		<mx:Spacer width="3%"/>
		<mx:VBox width="95%" height="100%">
		
			<mx:HBox width="100%" height="10%">
		
				<mx:Button width="9%" height="100%" click="{doSave()}" id="btnSave" enabled="{employee!=null}">
					<mx:icon>@Embed(source='../../../../assets/file.png')</mx:icon>
				</mx:Button>
				
				<mx:Button width="9%" height="100%" click="{doRevert()}" id="btnRevert" enabled="{employee!=null}">
					<mx:icon>@Embed(source='../../../../assets/revert.png')</mx:icon>
				</mx:Button>
				
				<mx:Button width="9%" height="100%" click="{closeWindow()}" id="btnClose">
					<mx:icon>@Embed(source='../../../../assets/cancel.png')</mx:icon>
				</mx:Button>
				
			</mx:HBox>
			
			<mx:HBox width="100%" height="10%">
			
				<mx:Label 
					text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Save')}"
					width="9%" textAlign="center"/>
				<mx:Label 
					text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Revert')}"
					width="9%" textAlign="center"/>
				<mx:Label 
					text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Close')}"
					width="9%" textAlign="center"/>
			
			</mx:HBox>
			
			<mx:VBox width="100%" height="80%">
			
				<mx:HBox width="100%" height="50%">
					
					<mx:Panel title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Status')}" layout="vertical" width="30%" height="100%">
					
						<mx:RadioButtonGroup id="activeStatus" enabled="{employee!=null}"/>
						
						<components:CustomRadioButtonComponent value="true" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Active')}" groupName="activeStatus"
							dataHolder="{employee}" variableName="active" customText="{employee.active}" enabled="{employee!=null}" disabledColor="#000000"/>
						
						<components:CustomRadioButtonComponent value="false" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Inactive')}" groupName="activeStatus"
							dataHolder="{employee}" variableName="active" customText="{employee.active}" enabled="{employee!=null}" disabledColor="#000000"/>
							
					</mx:Panel>
					
					<mx:Panel title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.AutoOut')}" layout="vertical" width="70%" height="100%">
					
						<mx:HBox width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
							
							<components:CustomTimeEntryStringComponent 
								showSeconds="false" enabled="{employee!=null}" functionName="setAutoHours"
								dataHolder="{employee}" variableName="autoOutTime" customText="{employee.autoOutTime}" is24Hour="true" disabledColor="#000000"/>
							
							<mx:TextInput id="autoOutText" enabled="false" text="{getAutoOutText(employee.autoOutTime)}"
								textAlign="center" disabledColor="#000000"/>
						</mx:HBox>
							
					</mx:Panel>
					
				</mx:HBox>
				
				<mx:Panel title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Message')}" layout="vertical" width="100%" height="50%">
				
					<components:CustomTextAreaComponent width="95%" height="50%" enabled="{employee!=null}"
						dataHolder="{employee}" variableName="message" customText="{employee.message}" disabledColor="#000000"/>
					
					<components:CustomCheckBoxComponent height="25%" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.ShowmessageatclockIN')}"
						dataHolder="{employee}" variableName="showMessageClockin" customText="{employee.showMessageClockin}"
						selectedValue="true" deSelectedValue="false" defaultValue="false" enabled="{employee!=null}" disabledColor="#000000"/>
						
					<components:CustomCheckBoxComponent height="25%" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.ShowmessageatclockOU')}"
						dataHolder="{employee}" variableName="showMessageClockout" customText="{employee.showMessageClockout}"
						selectedValue="true" deSelectedValue="false" defaultValue="false" enabled="{employee!=null}" disabledColor="#000000"/>
				</mx:Panel>
				
			</mx:VBox>
			
		</mx:VBox>	
		<mx:Spacer width="2%"/>
	</mx:HBox>
	
	<mx:DataGrid id="employeeDG" dataProvider="{employeeList}" width="100%" height="45%" itemClick="{setEmployeeObj(event)}">
		<mx:columns>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Status')}" dataField="active" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:Label text="{data.active?'Active':'Inactive'}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.ID')}" dataField="employeeId" textAlign="center"/>
			<mx:DataGridColumn width="300" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Name')}" dataField="lastName" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:Label text="{data.lastName}, {data.firstName}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.IN')}" dataField="clockIn" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox enabled="false" selected="{data.clockIn}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.OUT')}" dataField="clockOut" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox enabled="false" selected="{data.clockOut || (!data.clockIn &amp;&amp; !data.clockBreak)}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Break')}" dataField="clockBreak" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox enabled="false" selected="{data.clockBreak}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.Message')}" dataField="message" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox enabled="false" selected="{data.showMessageClockin || data.showMessageClockout}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
			<mx:DataGridColumn width="100" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeEmpClockOptionsCmd.AutoOut')}" dataField="autoHour" textAlign="center">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox enabled="false" selected="{data.autoHour!=0 || data.autoMin!=0}"/>
					</mx:Component>
				</mx:itemRenderer>
			</mx:DataGridColumn>
		</mx:columns>
	</mx:DataGrid>
		
</mx:TitleWindow>

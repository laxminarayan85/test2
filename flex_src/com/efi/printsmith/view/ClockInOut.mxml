<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="638" height="538" title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeKeeperCmd.ClockInOut')}" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeKeeperCmd.ClockInOut')}" 
	creationComplete="init()" xmlns:components="com.efi.printsmith.components.*" xmlns:vo="com.efi.printsmith.vo.*" showCloseButton="true" close="PopUpManager.removePopUp(this) ">
	<mx:RemoteObject id="dataService" destination="dataService" showBusyCursor="true">
		<mx:method name="getActiveEmployees" fault="handleFault(event)" result="handleLoadResult(event)"/>
		<mx:method name="addUpdate" fault="handleFault(event)" result="handleSaveResult(event)"/>
		<mx:method name="deleteItem" fault="handleFault(event)" result="handleDeleteResult(event)"/>
	</mx:RemoteObject>

	<mx:RemoteObject id="dataServiceTimeCard" destination="dataService" showBusyCursor="true">
		<mx:method name="addUpdate" fault="handleFault(event)" result="handleSaveTimeCardResult(event)"/>
		<mx:method name="getByClockInOut" fault="handleFault(event)" result="handleLoadTimeCardResult(event)"/>
	</mx:RemoteObject>
	
	<mx:RemoteObject id="dataServicePeriod" destination="dataService" showBusyCursor="true">
		<mx:method name="getByPeriodOpen" fault="handleFault(event)" result="handlePeriodLoadResult(event)"/>
	</mx:RemoteObject>
	

	
	<mx:Producer id="producer" destination="employee"/>
	<mx:Consumer id="consumer" destination="employee" message="messageHandler(event.message)"/>
	<mx:Consumer id="notification_consumer" destination="notifications" message="messageHandler(event.message)"/>
	
<mx:Script>  	
<![CDATA[
	import com.efi.printsmith.model.AutoClockOutDetector;
	import mx.utils.StringUtil;
	import com.efi.printsmith.data.Period;
	import com.efi.printsmith.events.PinRequestEvent;
	import com.efi.printsmith.pages.PinRequestComponent;
	import com.efi.printsmith.data.TimeCard;
	import mx.controls.Alert;                                         
	import mx.managers.PopUpManager;                                         
	import mx.containers.TitleWindow;                                         
	import mx.collections.ArrayCollection;                                         
	import mx.rpc.events.ResultEvent;                                         
	import mx.rpc.events.FaultEvent;                                         
	import mx.messaging.messages.AsyncMessage;
	import mx.messaging.messages.IMessage;
	import mx.printing.PrintAdvancedDataGrid;
    import mx.printing.PrintDataGrid;
    import mx.core.Application;
	import com.efi.printsmith.data.Employee;
    import com.efi.printsmith.data.City;
	import com.efi.printsmith.data.Country;
	import com.efi.printsmith.data.State;
	import com.efi.printsmith.data.Zip;

	[Bindable]
	private var employeesArray:ArrayCollection = new ArrayCollection();

	[Bindable]
	private var timecardArray:ArrayCollection = new ArrayCollection();

	[Bindable] 
	private var employee:Employee;

	[Bindable] 
	private var timecard:TimeCard;
	
	[Bindable] private var period:Period;
	
	[Bindable] public var autoClockOutDetectortList:ArrayCollection = new ArrayCollection();

	[Bindable]
	private var OnOffBreak:String="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeKeeperCmd.OnBreak')}";
	
	[Bindable] public var timeCardStatus:String = "";
		
	public function init():void {
		consumer.subscribe();
		notification_consumer.subscribe();
		PopUpManager.centerPopUp(this);
		var application:Snowmass = Snowmass.getInstance();
		clockIn.enabled = false;
		clockOut.enabled = false;
		clockBreak.enabled = false;
		dataService.getActiveEmployees();
		dataServicePeriod.getByPeriodOpen("Period");
		autoClockOutDetectortList = parentDocument.autoClockOutDetectortList;
		//user = application.currentUser;
	} 
	
	private function messageHandler(message:IMessage):void
	{
		var messageBody:Object = message.body;// as com.efi.messaging.MessageBody;
		
		var messageType:String = messageBody.messageType;
		if (messageType == "ADDUPDATE" || messageType == "DELETE") {
			var payloadType:String = messageBody.payloadType as String; // What kind of item was added/deleted
			var payload:Number = messageBody.payload as Number; // ID if item added or deleted
			if (payloadType == "Employee") {
		  		dataService.getActiveEmployees();
			}
		}
	}
	
	private function handlePeriodLoadResult(event:ResultEvent):void {
		var periodList:ArrayCollection = event.result as ArrayCollection;
		if(periodList!=null && periodList.length>0){
			period = periodList.getItemAt(0,0) as Period;
		}
	}
	
	private function handleLoadResult(ev:ResultEvent):void {
		employeesArray = ev.result as ArrayCollection;
	}
	private function handleSaveResult(ev:ResultEvent):void {
		dataService.getActiveEmployees();
	}
	private function handleDeleteResult(ev:ResultEvent):void {
		dataService.getActiveEmployees();
	}  
	
	private function handleSaveTimeCardResult(ev:ResultEvent):void {
		dataServiceTimeCard.getByClockInOut("TimeCard",employee);
	}
	private function handleLoadTimeCardResult(ev:ResultEvent):void {
		timecardArray = ev.result as ArrayCollection;
		triggerAutoClockOutDetector();
		
	}
	
	private function setEmployee():void
	{	
		dataService.addUpdate(employee);
	}
	
	private function setTimeCard(reason:String):void {

		var newCard:TimeCard;
		var item:TimeCard;
		
		if ((reason == "in") ||(reason == "out"))
		{

			if (timecardArray == null || timecardArray.length == 0)
			{
				newCard = new TimeCard();
				if(period!=null){
					newCard.period = period.periodNumber;
				}
			}else {
				newCard = timecardArray.getItemAt(0,0) as TimeCard;
			}
			if (reason == "in")
			{
				newCard.startDateTime = new Date(); 
				newCard.endDateTime = null;
				newCard.breakTime = null
				newCard.employee = employee;
				newCard.onClock = 1;
			}
			if (reason == "out")
			{
				newCard.endDateTime = new Date();
				newCard.onClock = 0;
			}
		} else {
			var totalBreakTime:Number = 0;
			newCard = timecardArray.getItemAt(0,0) as TimeCard;
			if(reason=="breakstop"){
				totalBreakTime = (new Date().time-newCard.breakTime.time)+convertStringToMilliseconds(newCard.breakHour);
				newCard.breakHour = convertMillisecondsToString(totalBreakTime);
			} else if(reason=="breakstart"){
				var breakstart:Date=new Date();
				newCard.breakTime = breakstart;
			} else if(reason=="breakstopwithclockout"){
				var stopTime:Date = new Date();
				totalBreakTime = (stopTime.time-newCard.breakTime.time)+convertStringToMilliseconds(newCard.breakHour);
				newCard.breakHour = convertMillisecondsToString(totalBreakTime);
				newCard.endDateTime = stopTime;
				newCard.onClock = 0;
			}
		}
			
		dataServiceTimeCard.addUpdate(newCard);
	}
	
	private function convertStringToMilliseconds(timeStr:String):Number {
		var milliSeconds:Number = 0;
		if(timeStr==null||timeStr==""){
			milliSeconds = 0;
		} else {
			if(timeStr.indexOf(":")>-1){
				var hours:Number = Number(timeStr.split(":")[0]);
				var minutes:Number = Number(timeStr.split(":")[1]);
				milliSeconds = (hours*60*60*1000)+(minutes*60*1000);
			} else {
				milliSeconds = 0;
			}
		}
		return milliSeconds;
	}
	
	private function convertMillisecondsToString(milliSeconds:Number):String {
		var txtString:String = "";
		if(milliSeconds<60000){
			txtString = "00:00";
		}
		if(milliSeconds>=60000 && milliSeconds<60000*60){
	    	txtString = "00:";
	    	txtString = txtString + numFormatter.format(milliSeconds/60000);
	    }
	    if(milliSeconds>=60000*60){
	    	txtString = numFormatter.format(milliSeconds/(60000*60))+":";
	    	txtString = txtString+numFormatter.format(milliSeconds%(60000*60)/(60000));
	    }
	    return txtString;
	}

	private function doClockIn(): void{  
		if (employee != null){
			if(employee.pinRequired){
				var pinRequestComponent:PinRequestComponent = PopUpManager.createPopUp(this,PinRequestComponent,true) as PinRequestComponent;
				pinRequestComponent.employee = employee;
				pinRequestComponent.addEventListener(PinRequestEvent.PINREQUEST,function(event:PinRequestEvent):void{
					if(event.success) {
						clockInUpdate();
					}
				},false,0,true);
			} else {
				clockInUpdate();
			}
		}
	}
	
	private function clockInUpdate():void {
		if (employee.clockBreak == true)
		{
			timeCardStatus = "breakstop";
			setTimeCard("breakstop");
		} else {
			if(employee.showMessageClockin && employee.message!=null && StringUtil.trim(employee.message)!="") {
				Alert.show(employee.message);
			}
			timeCardStatus = "in";
			setTimeCard("in");	
		}
		employee.clockIn= true;
		employee.clockOut = false;
		employee.clockBreak= false;
		setEmployee();
		clockIn.enabled = false;
		clockOut.enabled = false;
		clockBreak.enabled = false;
	}
	
	private function triggerAutoClockOutDetector():void {
		var count:int=-1;
		if(timeCardStatus=="in"){
			for each(var autoClockOutDetectorObj:AutoClockOutDetector in autoClockOutDetectortList){
				if(autoClockOutDetectorObj.employee.id==employee.id){
					count++;
					break;	
				}
			}
			if(count!=-1){
				var autoClockOutDetectorObject:AutoClockOutDetector = autoClockOutDetectortList.getItemAt(count) as AutoClockOutDetector;
				autoClockOutDetectorObject.stop();
				autoClockOutDetectortList.removeItemAt(count);
			}
			if(employee.autoHour!=0 || employee.autoMin!=0){
				var autoClockOutDetector:AutoClockOutDetector = new AutoClockOutDetector();
				autoClockOutDetector.employee = employee;
				autoClockOutDetector.timeCard =	timecardArray.getItemAt(0,0) as TimeCard; 
				autoClockOutDetector.start();
				autoClockOutDetectortList.addItem(autoClockOutDetector);
			}
			timeCardStatus = "";
		}
	}
	  
	private function doClockOut(): void{ 
		if (employee != null){
			if(employee.pinRequired){
				var pinRequestComponent:PinRequestComponent = PopUpManager.createPopUp(this,PinRequestComponent,true) as PinRequestComponent;
				pinRequestComponent.employee = employee;
				pinRequestComponent.addEventListener(PinRequestEvent.PINREQUEST,function(event:PinRequestEvent):void{
					if(event.success) {
						clockOutUpdate();
					}
				},false,0,true);
			} else {
				clockOutUpdate();
			}
		}
	}
	
	private function clockOutUpdate():void {
		if (employee.clockBreak == true)
		{
			timeCardStatus = "breakstopwithclockout";
			setTimeCard("breakstopwithclockout");
		} else {
			timeCardStatus = "out";
			setTimeCard("out");
		}
		if(employee.showMessageClockout && employee.message!=null && StringUtil.trim(employee.message)!="") {
			Alert.show(employee.message);
		}
		employee.clockIn= false;
		employee.clockOut = true;
		employee.clockBreak= false;
		setEmployee();
		clockIn.enabled = false;
		clockOut.enabled = false;
		clockBreak.enabled = false;
		var count:int=-1;
		for each(var autoClockOutDetectorObj:AutoClockOutDetector in autoClockOutDetectortList){
			if(autoClockOutDetectorObj.employee.id==employee.id){
				count++;
				break;	
			}
		}
		if(count!=-1){
			var autoClockOutDetectorObject:AutoClockOutDetector = autoClockOutDetectortList.getItemAt(count) as AutoClockOutDetector;
			autoClockOutDetectorObject.stop();
			autoClockOutDetectortList.removeItemAt(count);
		}
	}
	
	private function doClockBreak(): void{ 
		if (employee != null){
			if(employee.active){
				if(employee.pinRequired){
					var pinRequestComponent:PinRequestComponent = PopUpManager.createPopUp(this,PinRequestComponent,true) as PinRequestComponent;
					pinRequestComponent.employee = employee;
					pinRequestComponent.addEventListener(PinRequestEvent.PINREQUEST,function(event:PinRequestEvent):void{
						if(event.success) {
							clockBreakUpdate();
						}
					},false,0,true);
				} else {
					clockBreakUpdate();
				}
			}
		}
	}
	
	private function clockBreakUpdate():void {
		if (employee.clockBreak == true)
		{
			timeCardStatus = "breakstop";
			setTimeCard("breakstop");
			employee.clockIn= true;
			employee.clockOut = false;
			employee.clockBreak= false;
		}
		else
		{
			timeCardStatus = "breakstart";
			setTimeCard("breakstart");
			employee.clockIn= false;
			employee.clockOut = false;
			employee.clockBreak= true;
		}
		setEmployee();
		clockIn.enabled = false;
		clockOut.enabled = false;
		clockBreak.enabled = false;
	}
	
	private function handleFault(ev:FaultEvent):void {  
		var message:String;
		              
		message = "Error: "                          
		+ ev.fault.faultCode + " - "                                  
		+ ev.fault.faultDetail + " - "                                  
		+ ev.fault.faultString;
		Alert.show(message, "Error", 0, null, null, null, 4);                                 
	}

	private function doGrid(): void{ 
		employee = employeeGrid.selectedItem as Employee;
		dataServiceTimeCard.getByClockInOut("TimeCard",employee);
		if(employee.clockIn){
			clockIn.enabled = false;
			clockOut.enabled = true;
			clockBreak.enabled = true;
		} else if(employee.clockOut){
			clockIn.enabled = true;
			clockOut.enabled = false;
			clockBreak.enabled = false;
		} else if(employee.clockBreak){
			clockIn.enabled = true;
			clockOut.enabled = true;
			clockBreak.enabled = false;
		} else {
			clockIn.enabled = true;
			clockOut.enabled = false;
			clockBreak.enabled = false;
		}
		if (employee.clockBreak == true){
			OnOffBreak = "{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeKeeperCmd.OffBreak')}"
		}else {
			OnOffBreak = "{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeKeeperCmd.OnBreak')}"
		}
		
	}  
              
]]>
                             
</mx:Script>

	<mx:NumberFormatter id="numFormatter" precision="0" rounding="none"/>

	<mx:Button x="20" y="37" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeClockInOutCmd.ClockIn')}" 
		width="132" height="107" icon="@Embed(source='../../../../assets/ClockIn.png')" id="clockIn" click="doClockIn();"/>
	<mx:Button x="20" y="165" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeClockInOutCmd.ClockOut')}" 
		width="132" height="107" icon="@Embed(source='../../../../assets/ClockOut.png')" id="clockOut" click="doClockOut();"/>
	<mx:Button x="20" y="310" label="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeClockInOutCmd.Break')}" 
		width="132" height="107" id="clockBreak" icon="@Embed(source='../../../../assets/ClockOnBreak.png')" click="doClockBreak();"/>
	<mx:AdvancedDataGrid x="167" y="37" width="100%" id="employeeGrid" dataProvider="{employeesArray}" height="380" sortExpertMode="true"
		itemClick="doGrid()">
		<mx:groupedColumns>
			<mx:AdvancedDataGridColumn width="50" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeExportsCmd.FirstName')}" sortable="false" textAlign="center" dataField="firstName" />
			<mx:AdvancedDataGridColumn width="50" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeExportsCmd.LastName')}" sortable="false" textAlign="center" dataField="lastName"/>
				<mx:AdvancedDataGridColumnGroup id="Inout" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'custRepCmd.Status')}" sortable="false" editable="false">
				<mx:AdvancedDataGridColumn width="20" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeCardEditorCmd.Out')}" 
					sortable="false" textAlign="center"  editable="false" dataField="clockOut">
					<mx:itemRenderer>
						<mx:Component>
							<mx:CheckBox enabled="false" selected="{data.clockOut || (!data.clockIn &amp;&amp; !data.clockBreak)}"/>
						</mx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>	
				<mx:AdvancedDataGridColumn width="20" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeCardEditorCmd.In')}" 
					sortable="false" textAlign="center" editable="false" dataField="clockIn">
					<mx:itemRenderer>
						<mx:Component>
							<mx:CheckBox enabled="false" selected="{data.clockIn}"/>
						</mx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>	
				<mx:AdvancedDataGridColumn width="20" headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'employeeTimeCardEditorCmd.BreakTime')}" 
					sortable="false" textAlign="center" editable="false" dataField="clockBreak">
					<mx:itemRenderer>
						<mx:Component>
							<mx:CheckBox enabled="false" selected="{data.clockBreak}"/>
						</mx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>	
			</mx:AdvancedDataGridColumnGroup>
		</mx:groupedColumns>
	</mx:AdvancedDataGrid>
	
</mx:TitleWindow>

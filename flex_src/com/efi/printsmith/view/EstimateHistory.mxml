<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:vo="com.efi.printsmith.vo.*" label="Estmate History" name="Estmate History"
	width="100%" height="100%" headerHeight="0"  creationComplete="init()" layout="vertical"
    title="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rStandardMenuText.EstimateHistory')}">
    
	<mx:RemoteObject id="dataService" destination="dataService" showBusyCursor="true">
		<mx:method name="getAccountPicker" fault="handleFault(event)" result="handleLoadAccountsResult(event)"/>
		<mx:method name="getEstimateByAccountId" fault="handleFault(event)" result="handleLoadEstimatesResult(event)"/>
	</mx:RemoteObject>
	
	<mx:RemoteObject id="dataServiceInvoice" destination="dataService" showBusyCursor="true">
		<mx:method name="getInvoice" fault="handleFault(event)" result="handleLoadSingleEstimateResult(event)"/>
		<mx:method name="saveInvoice" fault="handleFault(event)" result="handleSaveEstimateResult(event)"/>
	</mx:RemoteObject>
	
	<mx:Script source="../common/scripts/CommonUtils.as"/>
		
	<mx:Script>                                   
		<![CDATA[
			import com.efi.printsmith.events.CopyToNewEstimateEvent;
			import com.efi.printsmith.events.CopyToNewInvoiceEvent;
			import com.efi.printsmith.events.commandEvents.EditForms.EditEditEstimateEvent;
			import com.efi.printsmith.pages.EstimateHistoryProperties;
			import com.efi.printsmith.data.Estimate;
			import mx.utils.StringUtil;
			import com.efi.printsmith.data.InvoiceBase;
			import com.efi.printsmith.events.EstimateHistorySaveEvent;
			import com.efi.printsmith.pages.EstimateNotesView;
			import mx.controls.dataGridClasses.DataGridColumn;
			import com.efi.printsmith.common.components.CommonLoadComponent;
			import com.efi.mdi.view.window.MDIWindow;
			import mx.controls.Alert;                                         
			import mx.managers.PopUpManager;                                         
			import mx.containers.TitleWindow;                                         
			import mx.collections.ArrayCollection;                                         
			import mx.rpc.events.ResultEvent;                                         
			import mx.rpc.events.FaultEvent;                                           
			import mx.messaging.messages.AsyncMessage;
			import mx.messaging.messages.IMessage;
		    import mx.printing.FlexPrintJob;
		    import mx.printing.PrintAdvancedDataGrid;
		    import mx.printing.PrintDataGrid;
		    import mx.core.Application;
		    import mx.validators.Validator;
		    import mx.events.ValidationResultEvent;    
			import com.efi.printsmith.data.Account; 
			import com.efi.printsmith.data.Invoice;         
			import com.efi.printsmith.events.AccountPickerSelectEvent;	     
			
			[Bindable]                                         
			private var account:Account;
			
			[Bindable]
		    private var estimates:ArrayCollection = new ArrayCollection(); 
		    
		    [Bindable] private var accounts:ArrayCollection;
		    
		    [Bindable] private var loadComponent:CommonLoadComponent;
		    
		    [Bindable] private var estimate:Estimate;
		    
		    private var estimateNotes:String = "";
		    
		    private var pageStatus:String = "";
			
			public function init():void {
				loadComponent = PopUpManager.createPopUp(this,CommonLoadComponent,true) as CommonLoadComponent;
				var mdiWin:MDIWindow = Snowmass.getInstance().containerManager.getWindowWithChild(this);
				mdiWin.title = resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rStandardMenuText.EstimateHistory');
				dataService.getAccountPicker();		
			}  
			
			private function handleLoadAccountsResult(event:ResultEvent):void {
				if(loadComponent){
					loadComponent.close();
				}
				accounts = event.result as ArrayCollection;
				doGet();
			}
			private function doGet():void {
				var accountPickerWindow:AccountPicker = AccountPicker(PopUpManager.createPopUp(this, AccountPicker, true));
			    accountPickerWindow.setStyle("borderAlpha", 0.9);
			    accountPickerWindow.formIsEmpty = true;
			    accountPickerWindow.prospect_boolean = true;
			    accountPickerWindow.showContactButton = false;
			    accountPickerWindow.setAccounts(accounts);
			    accountPickerWindow.addEventListener(AccountPickerSelectEvent.CANCELSELECTACCOUNT, handleAccountPickerCancel);
			    accountPickerWindow.addEventListener(AccountPickerSelectEvent.SELECTACCOUNT, handleAccountPickerSelect);
			} 
			 
			private function handleAccountPickerCancel(evt:AccountPickerSelectEvent):void {
				
			}
			
			private function handleAccountPickerSelect(evt:AccountPickerSelectEvent):void {
				account = evt.account;
				dataService.getEstimateByAccountId("Estimate", this.account.id);
			}
					
			private function handleLoadEstimatesResult(ev:ResultEvent):void {
				estimates = ev.result as ArrayCollection;
			}
			
			private function handleFault(event:FaultEvent):void {
				if(loadComponent!=null){
					loadComponent.close();
				}
				pageStatus = "";
				var message:String;
				              
				message = "Error: "                          
				+ event.fault.faultCode + " - "                                  
				+ event.fault.faultDetail + " - "                                  
				+ event.fault.faultString;
				Alert.show(message, "Error", 0, null, null, null, 4);        	
			} 
			
			private function getFormattedDate(item:Object,column:DataGridColumn):String {
				return dateFormatter.format(item[column.dataField]);
			}
			
			private function showNotes():void {
				if(estimate_grid.selectedIndex>-1 && estimate_grid.selectedItem!=null){
					pageStatus = "notes";
					dataServiceInvoice.getInvoice(estimate_grid.selectedItem.id);
				}
			} 
			
			private function showProperties():void {
				if(estimate_grid.selectedIndex>-1 && estimate_grid.selectedItem!=null){
					pageStatus = "properties";
					dataServiceInvoice.getInvoice(estimate_grid.selectedItem.id);
				}
			}
			
			private function saveEstimates(event:EstimateHistorySaveEvent):void {
				if(event.estimate!=null && event.estimate.id!=0) {
					estimate = event.estimate;
					dataServiceInvoice.saveInvoice(estimate);
				}
			}
			
			private function handleLoadSingleEstimateResult(event:ResultEvent):void {
				estimate = event.result as Estimate;
				if(pageStatus=="notes"){
					pageStatus = "";
					var estimateNotesView:EstimateNotesView = PopUpManager.createPopUp(this,EstimateNotesView,true) as EstimateNotesView;
					estimateNotesView.estimate = estimate;
					estimateNotesView.addEventListener(EstimateHistorySaveEvent.ESTIMATES_SAVE_EVENT,saveEstimates,false,0,true);
				} else if(pageStatus=="properties"){
					pageStatus = "";
					var estimateHistoryProperties:EstimateHistoryProperties = PopUpManager.createPopUp(this,EstimateHistoryProperties,true) as EstimateHistoryProperties;
					estimateHistoryProperties.estimate = estimate;
					estimateHistoryProperties.addEventListener(EstimateHistorySaveEvent.ESTIMATES_SAVE_EVENT,saveEstimates,false,0,true);
				} else {
					doEditEstimate(estimate);
				}
			}
			
			private function handleSaveEstimateResult(event:ResultEvent):void {
				openConfirmationComponent(StringUtil.substitute(resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'CommonConfirmation.SAVE'),resourceManager.getString(Snowmass.RESOURCE_BUNDLE,'newInvCmd.Estimate')));
				if(account!=null){
					dataService.getEstimateByAccountId("Estimate", this.account.id);
				}
			}
			
			private function doDoubleClick():void {
				var invoiceBase:InvoiceBase = estimate_grid.selectedItem as InvoiceBase;
				dataServiceInvoice.getInvoice(invoiceBase.id);
			}
			
			private function doEditEstimate(inv:InvoiceBase):void	{
				if (Snowmass.getInstance().security.commandEnabled(EditEditEstimateEvent.COMMANDID)) {
					var child:EditInvoice = new EditInvoice();
					child.setInvoice(inv);
					child.addEventListener(CopyToNewInvoiceEvent.COPY_TO_NEWINVOICE_EVENT,handleCopyToNewInvoiceResult,false,0,true);
					child.addEventListener(CopyToNewEstimateEvent.COPY_TO_NEWESTIMATE_EVENT,handleCopyToNewEstimateResult,false,0,true);
					var parentId:int = Snowmass.getInstance().containerManager.getWindowWithChild(this).windowID;
					Snowmass.getInstance().containerManager.openNewMDIWindow(child.title, child,-1,-1,-1,-1, parentId);
				}
			}
			
			private function handleCopyToNewInvoiceResult(event:CopyToNewInvoiceEvent):void {
				if(event.invoice!=null){
					var child:EditInvoice = new EditInvoice();
					child.invoice = event.invoice;
					var parentId:int = Snowmass.getInstance().containerManager.getWindowWithChild(this).windowID;
					Snowmass.getInstance().containerManager.openNewMDIWindow(child.title, child,-1,-1,-1,-1, parentId);
				}
			}
			
			private function handleCopyToNewEstimateResult(event:CopyToNewEstimateEvent):void {
				if(event.estimate!=null){
					var child:EditInvoice = new EditInvoice();
					child.invoice = event.estimate;
					var parentId:int = Snowmass.getInstance().containerManager.getWindowWithChild(this).windowID;
					Snowmass.getInstance().containerManager.openNewMDIWindow(child.title, child,-1,-1,-1,-1, parentId);
				}
			}
		]]>
	                             
	</mx:Script>
	
	<mx:DateFormatter id="dateFormatter" formatString="MM/DD/YYYY"/>
	
	<mx:HBox width="100%" height="8%">
		<mx:Button width="6%" height="100%" click="{doGet()}">
			<mx:icon>@Embed(source='../../../../assets/get.png')</mx:icon>
		</mx:Button>
		<mx:Button width="6%" height="100%">
			<mx:icon>@Embed(source='../../../../assets/find.png')</mx:icon>
		</mx:Button>
		<mx:Button width="6%" height="100%" click="{showNotes()}">
			<mx:icon>@Embed(source='../../../../assets/notes.png')</mx:icon>
		</mx:Button>
		<mx:Button width="6%" height="100%" click="{showProperties()}">
			<mx:icon>@Embed(source='../../../../assets/showCostCenter.png')</mx:icon>
		</mx:Button>
	</mx:HBox>
	
	<mx:HBox width="100%" height="4%">
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Get')}" width="6%" textAlign="center"/>
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'rButtonLabel.Find')}" width="6%" textAlign="center"/>
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Notes')}" width="6%" textAlign="center"/>
		<mx:Label text="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Properties')}" width="6%" textAlign="center"/>		
	</mx:HBox>
	
	<mx:DataGrid id="estimate_grid" width="100%" height="88%" dataProvider="{estimates}" doubleClick="{doDoubleClick()}" doubleClickEnabled="true">
		<mx:columns>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Created')}" dataField="created" labelFunction="getFormattedDate"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Estimate')}" dataField="invoiceNumber"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Title')}" dataField="name"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Total')}" dataField="grandTotal"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Status')}" dataField="col2"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.Invoice')}" dataField="convertedInvoiceNo"/>
			<mx:DataGridColumn headerText="{resourceManager.getString(Snowmass.RESOURCE_BUNDLE, 'estHistoryCmd.OffPending')}" dataField="offPendingDate" labelFunction="getFormattedDate"/>
		</mx:columns>
	</mx:DataGrid>

</mx:Panel>